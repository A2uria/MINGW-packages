# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=boost
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.57.0
_boostver=${pkgver//./_}
pkgrel=3
pkgdesc="Free peer-reviewed portable C++ source libraries (mingw-w64)"
arch=('any')
url="http://www.boost.org/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-icu"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3")
options=('strip' 'buildflags' 'staticlibs')
source=(http://downloads.sourceforge.net/sourceforge/boost/boost_${_boostver}.tar.bz2
        boost-1.48.0-add-bjam-man-page.patch
        boost-1.50.0-fix-non-utf8-files.patch
        boost-1.50.0-pool.patch
        boost-1.54.0-bootstrap.patch
        boost-1.54.0-locale-unused_typedef.patch
        boost-1.54.0-pool-max_chunks_shadow.patch
        boost-1.54.0-python-unused_typedef.patch
        boost-1.55.0-python-test-PyImport_AppendInittab.patch
        boost-1.56-mingw-context.patch
        boost-1.57.0-build-optflags.patch
        boost-1.57.0-move-is_class.patch
        boost-1.57.0-mpl-print.patch
        boost-1.57.0-pool-test_linking.patch
        boost-1.57.0-python-abi_letters.patch
        boost-1.57.0-python-libpython_dep.patch
        boost-1.57.0-signals2-weak_ptr.patch
        boost-1.57.0-spirit-unused_typedef.patch
        boost-1.57.0-uuid-comparison.patch
        boost-include-intrin-h-on-mingw-w64.patch
        using-mingw-w64-python.patch
        msys2-mingw-folders-bootstrap.patch)
md5sums=('1be49befbdd9a5ce9def2983ba3e7b76'
         'dff8166486150121bf89d7050c7e5c49'
         'ef427ef3a47f8fdeeb6b4ae3246aa84f'
         '957b9ec07e4197b87a0f1073e565d955'
         '45dbfa0c971c8625c5cb985e6ed71b5d'
         '18963bb39f778b0f0ed7894a5b48845d'
         'aac88d8444c25db38e6fc9f6e549ea4b'
         '4e652869f58b4ce601715b9fb2c2b9ee'
         'a08837900284c677cf1cff21636ecbe7'
         '2c65a8915b70b9de25ac37a73a74d84d'
         '2f7eaf049a7ab916008806beaf3eea0b'
         '65a7e8edffbed332162253d31301fa29'
         'e6631765436f87e90c16778a25d2129c'
         'c28bcd8eeafd494021ebcd80456a687a'
         'a128f4a4e371bdde469f055c7a045db7'
         'ff8ee6c27cc2f4d912f1c028d4f2d91d'
         '254c27857ee0574da377aa5ac107101c'
         'd8fb4b4bb69e225ff076513dfd756db9'
         '92f66bba767a61bb553a13a794270f28'
         '1f0904cea88ca36e6b927432c85676a3'
         'c2aac61b370b321552b5240b9ee7248e'
         '29ffd4b1636e31678fa8e1afa77305eb')

prepare() {
  cd "${srcdir}/boost_${_boostver}"
  
  patch -p1 -i ${srcdir}/boost-1.54.0-bootstrap.patch
  
  # https://svn.boost.org/trac/boost/ticket/6150
  patch -p1 -i ${srcdir}/boost-1.50.0-fix-non-utf8-files.patch

  # Add a manual page for bjam, based on the on-line documentation:
  # http://www.boost.org/boost-build2/doc/html/bbv2/overview.html
  patch -p1 -i ${srcdir}/boost-1.48.0-add-bjam-man-page.patch

  # https://bugzilla.redhat.com/show_bug.cgi?id=828856
  # https://bugzilla.redhat.com/show_bug.cgi?id=828857
  # https://svn.boost.org/trac/boost/ticket/6701
  patch -p1 -i ${srcdir}/boost-1.50.0-pool.patch

  # https://svn.boost.org/trac/boost/ticket/5637
  patch -p1 -i ${srcdir}/boost-1.57.0-mpl-print.patch

  # https://svn.boost.org/trac/boost/ticket/8870
  patch -p1 -i ${srcdir}/boost-1.57.0-spirit-unused_typedef.patch

  # https://svn.boost.org/trac/boost/ticket/8878
  patch -p1 -i ${srcdir}/boost-1.54.0-locale-unused_typedef.patch

  # https://svn.boost.org/trac/boost/ticket/8888
  patch -p1 -i ${srcdir}/boost-1.54.0-python-unused_typedef.patch

  # https://svn.boost.org/trac/boost/ticket/9038
  patch -p1 -i ${srcdir}/boost-1.57.0-pool-test_linking.patch

  # This was already fixed upstream, so no tracking bug.
  patch -p1 -i ${srcdir}/boost-1.54.0-pool-max_chunks_shadow.patch

  # https://bugzilla.redhat.com/show_bug.cgi?id=1102667
  patch -p1 -i ${srcdir}/boost-1.57.0-python-libpython_dep.patch
  patch -p1 -i ${srcdir}/boost-1.57.0-python-abi_letters.patch
  patch -p1 -i ${srcdir}/boost-1.55.0-python-test-PyImport_AppendInittab.patch

  # https://svn.boost.org/trac/boost/ticket/10100
  # https://github.com/boostorg/signals2/pull/8
  patch -p1 -i ${srcdir}/boost-1.57.0-signals2-weak_ptr.patch

  # https://bugzilla.redhat.com/show_bug.cgi?id=1190039
  patch -p1 -i ${srcdir}/boost-1.57.0-build-optflags.patch

  # https://svn.boost.org/trac/boost/ticket/10510
  patch -p1 -i ${srcdir}/boost-1.57.0-uuid-comparison.patch

  # https://bugzilla.redhat.com/show_bug.cgi?id=1192002
  # https://svn.boost.org/trac/boost/ticket/11044
  patch -p1 -i ${srcdir}/boost-1.57.0-move-is_class.patch

  # https://svn.boost.org/trac/boost/ticket/7262
  patch -p1 -i ${srcdir}/boost-1.56-mingw-context.patch
  
  # Fix FTBFS on recent mingw-w64 and also use intrinsics based
  # versions of the Interlocked symbols which are better optimized
  patch -p1 -i ${srcdir}/boost-include-intrin-h-on-mingw-w64.patch

  # Use out own build Python
  patch -p1 -i ${srcdir}/using-mingw-w64-python.patch
  
  patch -p1 -i ${srcdir}/msys2-mingw-folders-bootstrap.patch
}

build() {
  local PREFIX_DEPS=$(cygpath -am ${MINGW_PREFIX})

  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  lndir "${srcdir}/boost_${_boostver}" "${srcdir}/build-${MINGW_CHOST}"
  
  cd "${srcdir}/build-${MINGW_CHOST}"
  
  echo "using python : 2.7 : ${PREFIX_DEPS}/bin/python2.exe : ${PREFIX_DEPS}/include/python2.7 ;" >> ./tools/build/src/user-config.jam
  echo "using python : 3.4 : ${PREFIX_DEPS}/bin/python3.exe : ${PREFIX_DEPS}/include/python3.4m : ${PREFIX_DEPS}/lib/python3.4/config-3.4m ;" >> ./tools/build/src/user-config.jam
  #echo "using gcc : : ${PREFIX_DEPS}/bin/g++.exe : <rc>${PREFIX_DEPS}/bin/windres.exe ;" >> ./tools/build/src/user-config.jam

  ./bootstrap.sh --with-icu=${MINGW_PREFIX} --with-python=${MINGW_PREFIX} --with-toolset=gcc

  case ${CARCH} in
    i686)
      local _model=32
    ;;
    x86_64)
      local _model=64
    ;;
  esac

  if check_option "debug" "y"; then
      local variant=debug
  else
      local variant=release
  fi

  ./b2 \
    toolset=gcc \
    link=shared,static \
    address-model=${_model} \
    pch=off \
    runtime-link=shared \
    threading=multi \
    threadapi=win32 \
    variant=${variant} \
    --debug-configuration \
    --prefix=${pkgdir}${MINGW_PREFIX} \
    --layout=tagged \
    --without-mpi \
    -sHAVE_ICU=1 \
    -sICU_PATH=${MINGW_PREFIX} \
    -sICU_LINK="-L${MINGW_PREFIX}/lib -licuuc -licuin -licudt" \
    -sICONV_PATH=${MINGW_PREFIX} \
    -sICONV_LINK="-L${MINGW_PREFIX}/lib -liconv" \
    -sNO_BZIP2 \
    -sBZIP2_BINARY=bz2 \
    -sBZIP2_INCLUDE=${MINGW_PREFIX}/include \
    -sBZIP2_LIBPATH=${MINGW_PREFIX}/lib \
    -sNO_ZLIB \
    -sZLIB_BINARY=z \
    -sZLIB_INCLUDE=${MINGW_PREFIX}/include \
    -sZLIB_LIBPATH=${MINGW_PREFIX}/zlib \
    -d2
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  case ${CARCH} in
    i686)
      local _model=32
    ;;
    x86_64)
      local _model=64
    ;;
  esac

  if check_option "debug" "y"; then
      local variant=debug
  else
      local variant=release
  fi

  ./b2 \
      toolset=gcc \
      variant=${variant} \
      threading=multi \
      threadapi=win32 \
      link=shared,static \
      runtime-link=shared \
      --prefix=${pkgdir}${MINGW_PREFIX} \
      -sHAVE_ICU=1 \
      -sICU_PATH=${MINGW_PREFIX} \
      -sICU_LINK="-L${MINGW_PREFIX}/lib -licuuc -licuin -licudt" \
      -sICONV_PATH=${MINGW_PREFIX} \
      -sICONV_LINK="-L${MINGW_PREFIX}/lib -liconv" \
      -sNO_BZIP2 \
      -sBZIP2_BINARY=bz2 \
      -sBZIP2_INCLUDE=${MINGW_PREFIX}/include \
      -sBZIP2_LIBPATH=${MINGW_PREFIX}/lib \
      -sNO_ZLIB \
      -sZLIB_BINARY=z \
      -sZLIB_INCLUDE=${MINGW_PREFIX}/include \
      -sZLIB_LIBPATH=${MINGW_PREFIX}/zlib \
      address-model=${_model} \
      --layout=tagged install \
      --without-mpi

  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin
  mv ${pkgdir}${MINGW_PREFIX}/lib/*.dll ${pkgdir}${MINGW_PREFIX}/bin
}
