# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=creduce
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=creduce.2.2.0.18.g3f1d123
pkgrel=1
pkgdesc="A C program reducer (mingw-w64)"
depends=('delta'
         'perl-Benchmark-Timer'
         'perl-Exporter-Lite'
         'perl-File-Which'
         'perl-Getopt-Tabular'
         'perl-Regexp-Common'
         'perl-Sys-CPU'
         "${MINGW_PACKAGE_PREFIX}-astyle"
         "${MINGW_PACKAGE_PREFIX}-indent")
arch=('any')
url='https://github.com/csmith-project/creduce'
license=('custom:University of Illinois/NCSA Open Source License')
makedepends=('git' 'flex' "${MINGW_PACKAGE_PREFIX}-clang")
source=('git://github.com/csmith-project/creduce.git'
        "llvm-config-cygpath")
sha256sums=('SKIP'
            '03a7239d1e26d8a5dd5f405e10826679725cae6c01a9c37e80c6f1c184dad0a9')

prepare() {
  cd "$srcdir"
  [ -d tools ] && rm -rf tools
  mkdir tools
  cp -f "$srcdir"/llvm-config-cygpath tools/llvm-config
}

pkgver() {
  cd "$srcdir/$_realname"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir"
  [ -d ${CARCH} ] && rm -rf ${CARCH}
  mkdir ${CARCH}
  cd ${CARCH}
  PATH="$srcdir/tools:$PATH" \
    ../$_realname/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST}
  make
}

package() {
  cd "$srcdir/${CARCH}"
  make DESTDIR="$pkgdir" install
  install -Dm644 "../$_realname/COPYING" "$pkgdir${MINGW_PREFIX}/share/licenses/$_realname/COPYING"

  # Point creduce perl script:
  # lib $FindBin::Bin, '${MINGW_PREFIX}/share/creduce/perl';
  # ..  to msys2 perl.
  sed "s|${MINGW_PREFIX}|/usr|g" \
    -i $pkgdir${MINGW_PREFIX}/bin/creduce
}
