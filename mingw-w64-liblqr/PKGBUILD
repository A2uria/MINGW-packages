# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=liblqr
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.4.2
pkgrel=3
pkgdesc="A seam-carving C/C++ library called Liquid Rescale (mingw-w64)"
arch=('any')
url="http://liblqr.wikidot.com/"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-glib2")
options=('staticlibs' 'strip')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/carlobaldassi/liblqr/archive/v${pkgver}.tar.gz)
md5sums=('f7b4ecddd8a038f057e236347fd7767c')

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
