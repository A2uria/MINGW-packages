# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=libical-glib
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.2
pkgrel=1
arch=('any')
pkgdesc="An iCalendar library based on libical and introspectable by GObject Introspection (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-libical"
         "${MINGW_PACKAGE_PREFIX}-glib2"
		 "${MINGW_PACKAGE_PREFIX}-libxml2")
makedepends=("gtk-doc"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
options=('strip' 'staticlibs')
license=("LGPL 2.1")
url="http://www.gnome.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz
        001-win.patch)
sha256sums=('0ea3726bc9aff063b223d071465f2f87ef63507b92c8cb36e45a4e4e48de75a4'
            '4e34ccb69bd5356071b4d0ee96cf5b7148f6ae7dd3815861dee4fbb8e99156fd')

prepare() {
  cd ${_realname}-${pkgver}

  patch -b -V simple -p1 -i ${srcdir}/001-win.patch

  autoreconf -fi
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-introspection=yes
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
