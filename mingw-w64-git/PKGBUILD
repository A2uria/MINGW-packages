# Maintainer: Martell Malone <martellmalone@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=git

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
tag=2.3.4
pkgver=2.3.4.3.e992745
pkgrel=1
pkgdesc="The fast distributed version control system (mingw-w64)"
arch=('i686' 'x86_64')
url="http://git-for-windows.github.io/"
license=('GPL2')

makedepends=('python2' 'less' 'openssh' 'patch' 'make' 'tar' 'diffutils'
	'ca-certificates')

depends=("${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-expat>=2.0"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-pcre"
         "${MINGW_PACKAGE_PREFIX}-tcl"
         "perl-Error"
         "perl>=5.14.0"
         "perl-Authen-SASL"
         "perl-libwww"
         "perl-MIME-tools"
         "perl-Net-SMTP-SSL"
         "perl-TermReadKey")

source=("${_realname}"::"git+https://github.com/git-for-windows/git.git#tag=v$tag.windows.$pkgrel")

md5sums=('SKIP')

pkgver() {
  cd "$srcdir/git"
  printf "%s.%s.%s" "${tag}" "${pkgrel}" "$(git rev-parse --short HEAD)"
}

case "$CARCH" in
i686)
  SHAREDIR=/mingw32/share/git
  ;;
x86_64)
  SHAREDIR=/mingw64/share/git
  ;;
esac

build() {
  export PYTHON_PATH=/usr/bin/python2
  cd "$srcdir/git"

  make -j1 all &&

  (cd $startdir &&
   for basename in git-bash git-cmd git-wrapper gitk
   do
	windres -O coff $basename.rc -o $srcdir/git/$basename.res
   done
  )
  gcc -mwindows -s -O2 -Wall -o git-bash.exe git-bash.res \
	compat/win32/git-wrapper.o git.res -lshlwapi &&
  gcc -mconsole -s -O2 -Wall -o git-cmd.exe git-cmd.res \
	compat/win32/git-wrapper.o git.res -lshlwapi &&
  mkdir -p cmd &&
  gcc -mconsole -s -O2 -Wall -o cmd/git.exe git-wrapper.res \
	compat/win32/git-wrapper.o git.res -lshlwapi &&
  gcc -mwindows -s -O2 -Wall -o cmd/gitk.exe gitk.res \
	compat/win32/git-wrapper.o git.res -lshlwapi
}

package() {
  export PYTHON_PATH=/usr/bin/python2
  cd "$srcdir"/git

  make DESTDIR="$pkgdir" install

  # completions
  PREFIX="$(expr "$(cat < GIT-PREFIX)" : '[^:]*:[^:]*:[^:]*:\([^:]*\):[^:]*')"
  install -d "$pkgdir/$PREFIX/share/git/completion/"
  install contrib/completion/* "$pkgdir/$PREFIX/share/git/completion/"

  # subtree, for backwards-compatibility with MSys2's Git package
  make -C contrib/subtree prefix="$pkgdir/$PREFIX" install

  # Git Bash, Git CMD
  install -d -m755 $pkgdir$SHAREDIR
  install -m755 $startdir/git-for-windows.ico $pkgdir$SHAREDIR
  install -m755 git-bash.exe git-cmd.exe $pkgdir

  # Install Git wrapper into /cmd/
  install -d -m755 $pkgdir/cmd
  install -m755 cmd/git.exe cmd/gitk.exe $startdir/start-ssh-agent.cmd \
	$pkgdir/cmd
}
