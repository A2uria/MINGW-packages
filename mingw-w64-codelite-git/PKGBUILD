# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=codelite
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=7.0.157.ga86ea67
pkgrel=1
pkgdesc="Open-source, cross platform IDE for the C/C++ programming languages (mingw-w64)"
arch=('any')
license=('GPL')
url="http://www.codelite.org"
depends=("${MINGW_PACKAGE_PREFIX}-hunspell"
        "${MINGW_PACKAGE_PREFIX}-libssh"
        "${MINGW_PACKAGE_PREFIX}-wxWidgets"
        #"${MINGW_PACKAGE_PREFIX}-drmingw"
        "${MINGW_PACKAGE_PREFIX}-gcc-libs")
optdepends=("${MINGW_PACKAGE_PREFIX}-clang")
makedepends=("${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "git")
options=(strip staticlibs !debug)
source=("${_realname}::git+https://github.com/eranif/codelite.git"
        windows-headers-conflict.patch
        include-winsock-header.patch
        mingw-use-wx-manifests.patch
        use-gnu-extensions.patch
        mingw-posix-layout.patch
        codelite-php-linking.patch)
md5sums=('SKIP'
         '90b7cca657631d9aefbe0c6cf0af5f6c'
         '12ab2e97f45429259f93ca9c0c46784b'
         'd7ec793d6c39874f04c1c2d8f366005b'
         'f7ce106c3565835c019fbcda0f9bea56'
         'e8385c31ad144df0cc0fabb769dfb3fb'
         'f7f39966d3f36ae09c0536a72400ae7d')

pkgver() {
  cd "$_realname"
  git describe --tags | sed 's|-|.|g'
}

prepare() {
  cd "$srcdir/$_realname"
  patch -p1 -i ${srcdir}/windows-headers-conflict.patch
  patch -p1 -i ${srcdir}/include-winsock-header.patch
  patch -p1 -i ${srcdir}/mingw-use-wx-manifests.patch
  patch -p1 -i ${srcdir}/use-gnu-extensions.patch
  patch -p1 -i ${srcdir}/mingw-posix-layout.patch
  patch -p1 -i ${srcdir}/codelite-php-linking.patch
}

build() {
  local -a extra_config

  if check_option "debug" "y"; then
    extra_config+=( -DCMAKE_BUILD_TYPE=Debug )
  else
    extra_config+=( -DCMAKE_BUILD_TYPE=Release )
  fi

  cd $srcdir/${_realname}
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DPREFIX=${pkgdir}${MINGW_PREFIX} \
    -DENABLE_CLANG=1 \
    -DWITH_WEBVIEW=1 \
    -DwxWidgets_CONFIG_EXECUTABLE=${MINGW_PREFIX}/bin/wx-config \
    -DwxWidgets_wxrc_EXECUTABLE=${MINGW_PREFIX}/bin/wxrc-3.0 \
    "${extra_config[@]}" \
    .

  make # -j1 VERBOSE=1
}

package() {
  cd "$srcdir/${_realname}"
  make install

  rm -f ${pkgdir}${MINGW_PREFIX}/lib/codelite/*.a
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/*.a
}
