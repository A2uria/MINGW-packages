# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=glpk
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.55
pkgrel=2
pkgdesc="GNU Linear Programming Kit: solve LP, MIP and other problems (mingw-w64)"
arch=('any')
url="http://www.gnu.org/software/glpk/glpk.html"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-gmp")
options=('staticlibs' !strip debug)
source=("http://ftp.gnu.org/gnu/glpk/glpk-${pkgver}.tar.gz"
        'timeval-64bit.patch')
sha1sums=('893058aada022a8dfc63c675ebcd7e7e86a3a363'
          '18a4315cf751c0e1121d25dbb115aa6187324128')

prepare () {
  cd "${srcdir}/glpk-${pkgver}"
  sed -i "s|-version-info|-no-undefined -version-info|g" src/Makefile.am
  autoreconf -vfi
  patch -p1 -i "${srcdir}/timeval-64bit.patch"
}

build() {
  cd "${srcdir}"
  [ -d build-${CARCH} ] && rm -rf build-${CARCH}
  mkdir -p build-${CARCH} && cd build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --with-gmp \
    --with-gnu-ld

  make
}

check() {
  cd "${srcdir}/build-${CARCH}"
  make check
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
