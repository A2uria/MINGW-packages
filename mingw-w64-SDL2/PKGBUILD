# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=SDL2
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0.3
pkgrel=3
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2) (mingw-w64)"
arch=('any')
url="http://libsdl.org"
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-libiconv")
options=('staticlibs' 'strip')
source=("http://libsdl.org/release/SDL2-${pkgver}.tar.gz"
        fix-linker-flags.patch
        SDL2-prevent-duplicate-d3d11-declarations.patch
        SDL2-fix-gcc-compatibility.patch)
md5sums=('fe6c61d2e9df9ef570e7e80c6e822537'
         '932a4c42f068a9117c3c02efead14b29'
         'a70714d69deaea9e4f0cbfb17e9f4acf'
         'abbdbc90d737c8296b58d6af462b68b7')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/fix-linker-flags.patch
  patch -p1 -i ${srcdir}/SDL2-prevent-duplicate-d3d11-declarations.patch
  patch -p1 -i ${srcdir}/SDL2-fix-gcc-compatibility.patch

  ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
  sed -e "s|-mwindows||g" -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/sdl2.pc
}
