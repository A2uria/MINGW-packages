# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=qt-creator
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
__pre=
_base_ver=3.4.0
pkgver=${_base_ver}${_pre}
pkgrel=2
pkgdesc='Cross-patform IDE (mingw-w64)'
url='http://qt-project.org'
install=qt-creator-${CARCH}.install
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-qt5"
         "${MINGW_PACKAGE_PREFIX}-gcc"
         "${MINGW_PACKAGE_PREFIX}-make"
         "${MINGW_PACKAGE_PREFIX}-qbs")
makedepends=("${MINGW_PACKAGE_PREFIX}-clang")
options=('docs' 'staticlibs') # 'debug' '!strip')
optdepends=("${MINGW_PACKAGE_PREFIX}-gdb: for the debugger"
            "${MINGW_PACKAGE_PREFIX}-cmake: for cmake project support"
            "${MINGW_PACKAGE_PREFIX}-clang: for clang code model"
            #'openssh-askpass: for ssh support'
            #'git: for git support'
            #'mercurial: for mercurial support'
            #'bzr: for bazaar support'
            )
_pkgfqn="${_realname}-opensource-src-${_base_ver}${__pre}"
source=(#http://download.qt-project.org/development_releases/qtcreator/${_base_ver%.*}/${_base_ver}${__pre}/${_pkgfqn}.tar.gz
        http://download.qt-project.org/official_releases/qtcreator/${pkgver%.*}/${pkgver}/${_pkgfqn}.tar.gz
        0001-Qbs-Do-not-crash-when-using-simplified-project-view.patch
        0001-autotools-Set-a-default-buildDir.patch
        0002-Debugger-Handle-case-of-os.path.sep-being.patch
        0002-autotools-Use-summaryInWorkdir.patch
        0001-Debugger-Fix-jump-to-file-line-breakpoint-from-disas.patch
        qt-creator-3.2.0-Allow-static-clang-via-LLVM_STATIC-env-var.patch
        qt-creator-3.2.0-Allow-iOS-plugin-on-any-platform.patch
        qt-creator-3.2.0-Define-QBS_INSTALL_DIR-as-relative-path.patch
        qt-creator-3.2.0-qbs-CONFIG-add-qbs_enable_project_file_updates.patch
        qt-creator-3.2.0-MSYS2-autotools-run-shell-scripts-via-sh.patch
        qt-creator-3.2.0-autotools-allow-non-Automake-Makefile-in-projects.patch
        qt-creator-3.2.0-i[3456]86-and-x86-have-32bit-wordWidth.patch
        qt-creator-3.3.0-abi-msys-is-Windows.patch
        qt-creator-3.3.0-MinGW-w64-MIB_TCP_STATE-not-defined-until-Vista.patch)
noextract=(${_pkgfqn}.tar.gz)
sha256sums=('b80baf5be9b0421b3d951a8a0eb411a65cf008f4c753f5a80d205e90fa4fe112'
            '18f59946054e667b56276f53ec544d4786c3c249fee6c0bd9b0b565ed4b7cf2d'
            '205d8637152d6e6e57c4a9d9c5146efa7fa16db5fb7b8cabc3bc5ded96823d8a'
            '6217d0682c40ac586205abc835490b36ac79525fdefde5afd66e3b02daabd8a0'
            '22253cf29a2c8effebb1cc8313a0f350cf993d7fc404763063e6bd8444fa61f1'
            'f5652fc21b0ad7463f0286143a970292e0b5d05c980206639dc8f939973a580b'
            '45b9fc82c7a9364450420920adcdb04a3b67eeb96f64733166da757a5ab91013'
            '96c14f54577bf6cadf5c12018745666a9e99cd8d6a876c29a28b13599a8cb368'
            '68133c95b70c1b54e55ab4c8b31023afe4ab9a33667e4510bda64c828b95e91e'
            '1a1738379b0413484fd1ff3521b6bc85d034f5f964311d963dd044b4966b274b'
            '2915cd4bf08643057129c26c0ce28caee14586fdf6a164a96e36ce0b37966427'
            '0b0bba8bda0fd8247563e3827f58fe73baed17b09b9de6ea28375b7f1afa3ee1'
            '79fc4d2ef02ff8f458b1b330c683c2a86397f056369c90176a9ddbeba3b719fd'
            'c4cae708825102d538e1ecb9a88ecb216d366feb779553a2e91f97c4b78f8427'
            'b4cf07e2772b2ec6710ee7df6463feddb4195d65fa59e6c6095fbc0cb240417e')
prepare() {
  [ -d ${srcdir}/${_pkgfqn} ] && rm -rf ${srcdir}/${_pkgfqn}
  tar -xzf ${srcdir}/${_pkgfqn}.tar.gz -C $srcdir || true

  cd ${srcdir}/${_pkgfqn}

# Not needed for > 3.2.2
# patch -p1 -i "${srcdir}"/0001-Qbs-Do-not-crash-when-using-simplified-project-view.patch

# Attempting to upstream: https://codereview.qt-project.org/#/c/94366/
  patch -p1 -i "${srcdir}"/0001-autotools-Set-a-default-buildDir.patch
  patch -p1 -i "${srcdir}"/0002-Debugger-Handle-case-of-os.path.sep-being.patch

# patch -p1 -i "${srcdir}"/0002-autotools-Use-summaryInWorkdir.patch
  patch -p1 -i "${srcdir}"/0001-Debugger-Fix-jump-to-file-line-breakpoint-from-disas.patch

  patch -p1 -i "${srcdir}"/qt-creator-3.2.0-Allow-static-clang-via-LLVM_STATIC-env-var.patch
  patch -p1 -i "${srcdir}"/qt-creator-3.2.0-Allow-iOS-plugin-on-any-platform.patch
  patch -p1 -i "${srcdir}"/qt-creator-3.2.0-Define-QBS_INSTALL_DIR-as-relative-path.patch
  patch -p1 -i "${srcdir}"/qt-creator-3.2.0-qbs-CONFIG-add-qbs_enable_project_file_updates.patch

  patch -p1 -i "${srcdir}"/qt-creator-3.2.0-MSYS2-autotools-run-shell-scripts-via-sh.patch
  patch -p1 -i "${srcdir}"/qt-creator-3.2.0-autotools-allow-non-Automake-Makefile-in-projects.patch
  patch -p1 -i "${srcdir}"/qt-creator-3.2.0-i[3456]86-and-x86-have-32bit-wordWidth.patch
  patch -p1 -i "${srcdir}"/qt-creator-3.3.0-abi-msys-is-Windows.patch
  patch -p1 -i "${srcdir}"/qt-creator-3.3.0-MinGW-w64-MIB_TCP_STATE-not-defined-until-Vista.patch

  # Use system installed qbs
  rm -rf src/shared/qbs
}

build() {
  [ -d ${srcdir}/build-${MINGW_CHOST} ] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  export LLVM_INSTALL_DIR=${MINGW_PREFIX}
  export QBS_INSTALL_DIR=${MINGW_PREFIX}
  export LLVM_STATIC=1
  if check_option "debug" "y"; then
    _config_variant=debug
  else
    _config_variant=release
  fi
  ${MINGW_PREFIX}/bin/qmake.exe ../${_pkgfqn}/qtcreator.pro CONFIG+=${_config_variant}
  make ${_config_variant}
  make docs
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make INSTALL_ROOT="${pkgdir}${MINGW_PREFIX}" install
  make INSTALL_ROOT="${pkgdir}${MINGW_PREFIX}" install_docs

  install -Dm644 ${srcdir}/${_pkgfqn}/LGPL_EXCEPTION.TXT ${pkgdir}${MINGW_PREFIX}/share/licenses/qtcreator/LGPL_EXCEPTION.TXT
}

# Please leave this block here as I use
# it to quickly make and test changes.
# return 0
# Debugger ProjectExplorer 
# ALLGOOD="yes"
# PLUGINS="AutotoolsProjectManager"
# for PLUGIN in $PLUGINS; do
#   PLUGINLC=$(echo ${PLUGIN} | tr 'A-Z' 'a-z')
#   pushd src/build-x86_64-w64-mingw32/src/plugins/${PLUGINLC}
#     PATH=/mingw64/bin:"$PATH" make || ALLGOOD="no"
#   popd
# done
# if [ "${ALLGOOD}" = "yes" ]; then
# (
#   QTCPID=$(ps --windows | grep creator | awk '{ print $1; }')
#   while [ -n "$QTCPID" ]; do
#     cmd.exe /c "taskkill /PID $QTCPID"
#     sleep 1
#     QTCPID=$(ps --windows | grep creator | awk '{ print $1; }')
#   done
#   for PLUGIN in $PLUGINS; do
#     PLUGINLC=$(echo ${PLUGIN} | tr 'A-Z' 'a-z')
#     pushd src/build-x86_64-w64-mingw32/src/plugins/${PLUGINLC}
#       cp ../../../lib/qtcreator/plugins/${PLUGIN}d.dll /mingw64/lib/qtcreator/plugins/
#     popd
#   done
#   PATH=/mingw64/bin:$PATH /mingw64/bin/qtcreator &
#   )
# fi
