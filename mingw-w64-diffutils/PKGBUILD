# Maintainer: Alexey Pavlov <Alexpux@gmail.com>
# Contributor: Paul Moore <p.f.moore@gmail.com>

_realname=diffutils
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.3
pkgrel=1
pkgdesc="GNU Diffutils is a package of several programs related to finding differences between files (mingw-w64)"
arch=('any')
url='http://www.gnu.org/software/diffutils/'
license=('GPL3')
depends=("${MINGW_PACKAGE_PREFIX}-libwinpthread")
options=('strip' '!libtool' 'staticlibs')
source=("http://ftp.gnu.org/gnu/${_realname}/${_realname}-${pkgver}.tar.xz"
       "win_use_raise_rather_than_kill.patch"
       "win64_dont_check_ptrdiff_size.patch"
       "msvc_invalid_parameter_handler.patch")
md5sums=('99180208ec2a82ce71f55b0d7389f1b3'
         '6e979e13696642bd184fb6b946ba996a'
         '957454716c06313df2251cd2baf5a89c'
         'de6ece1a99a2471131899e49355f87e4')


prepare() {
  cd $srcdir/${_realname}-${pkgver}
  patch -p0 -i ${srcdir}/win_use_raise_rather_than_kill.patch
  patch -p0 -i ${srcdir}/win64_dont_check_ptrdiff_size.patch
  patch -p0 -i ${srcdir}/msvc_invalid_parameter_handler.patch
}

build() {
  cd $srcdir/${_realname}-${pkgver}
  ./configure --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make install DESTDIR="${pkgdir}"

  # Licenses
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/README" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/README"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
