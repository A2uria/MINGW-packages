# Maintainer: Oscar Fuentes <ofv@wanadoo.es>
# Contributor: Ray Donnelly <mingw.android@gmail.com>
# Based on Arch's PKGBUILD

_realname=tktable
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_pkgnameactual=Tktable
pkgver=2.10
pkgrel=4
pkgdesc="A full-featured 2D table widget for Tk (mingw-w64)"
arch=('any')
url="http://tktable.sourceforge.net/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-tk")
source=("http://sourceforge.net/projects/tktable/files/${_realname}/${pkgver}/${_pkgnameactual}${pkgver}.tar.gz"
        001-fix-detect-mingw.patch)
md5sums=('ecdd96b39ad49b820bf084eb1c786f66'
         'c1759a7b591ad70516166137dcbe3864')

prepare() {
  cd ${_pkgnameactual}${pkgver}
  patch -p1 -i ${srcdir}/001-fix-detect-mingw.patch
  
  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build-${CARCH}" ]] && rm -rf "${srcdir}/build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  [[ "${MINGW_CHOST}" = 'x86_64-w64-mingw32' ]] && enable64bit='--enable-64bit'

  ../${_pkgnameactual}${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    $enable64bit
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
