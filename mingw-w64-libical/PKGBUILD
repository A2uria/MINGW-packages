# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=libical
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.1
pkgrel=3
arch=('any')
pkgdesc="An Open Source implementation of the iCalendar protocols and protocol data units (mingw-w64)"
license=("LGPL 2.1")
url="https://github.com/libical/libical"
depends=()
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
options=('strip' '!debug' 'staticlibs')
source=("${_realname}-${pkgver}.tar.gz"::https://github.com/${_realname}/${_realname}/archive/v${pkgver}.tar.gz
        001-win.patch
        002-introspection.patch)
noextract=(${_realname}-${pkgver}.tar.gz)
sha256sums=('7d5f613454ec6c7d1bcfb441c919215be53292aa15cd1cb14249d1413d6c610c'
            'f080396da2203af16ed770141595fed86934df51c2ce40edfeccea23eaf25e00'
            '9dcd3c8fe152a5ce3e2a0b7983df879fb09bab6f3b81497b2530f274c744d31b')

prepare() {
  tar --exclude=ReadMe.md -zxf ${_realname}-${pkgver}.tar.gz

  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-win.patch
  patch -p1 -i ${srcdir}/002-introspection.patch
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
    -DCMAKE_C_FLAGS="${CFLAGS}" \
    -DGOBJECT_INTROSPECTION=OFF \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
