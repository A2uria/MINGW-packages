# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=cgal
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.6.1
pkgrel=2
_pkgid=34898
pkgdesc="Computational Geometry Algorithms Library (mingw-w64)"
arch=('any')
url="http://www.cgal.org"
license=("LGPL, GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-eigen3"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         "${MINGW_PACKAGE_PREFIX}-mpfr")
optdepends=("${MINGW_PACKAGE_PREFIX}-eigen3: for some packages, see the CGAL manual"
            #"${MINGW_PACKAGE_PREFIX}-qt4: for CGAL_Qt4"
           )
options=('strip' 'staticlibs')
source=(http://gforge.inria.fr/frs/download.php/${_pkgid}/CGAL-${pkgver}.tar.xz
        msys2-relocate-cmake-config.patch
        fix-library-location-config.patch)
sha1sums=('a187920223ba3b2a275982e43bec33fadcea01db'
          '3742f2bb189f6c53e66e6ba5562c28685f9d1a86'
          '36631de181f3c4a1f5a06c93633fedbe3503bafc')

prepare() {
  cd "${srcdir}"/CGAL-${pkgver}
  patch -p1 -i "${srcdir}"/msys2-relocate-cmake-config.patch
  patch -p1 -i "${srcdir}"/fix-library-location-config.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
      -G"MSYS Makefiles" \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_BUILD_TYPE=Release \
      ../CGAL-${pkgver}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
  local MINGW_PREFIX_WIN=$(cygpath -am ${MINGW_PREFIX})
  sed -s "s|${MINGW_PREFIX_WIN}|\${RELOCATED_INSTALL_PREFIX}|g" -i "${pkgdir}"${MINGW_PREFIX}/lib/CGAL/CGALConfig.cmake
  sed -s "s|${MINGW_PREFIX}|\${RELOCATED_INSTALL_PREFIX}|g" -i "${pkgdir}"${MINGW_PREFIX}/lib/CGAL/CGALConfig.cmake
}
