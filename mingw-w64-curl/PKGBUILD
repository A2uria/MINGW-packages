# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=curl
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-pdb")
pkgver=7.61.0
pkgrel=2
pkgdesc="Command line tool and library for transferring data with URLs. (mingw-w64)"
arch=('any')
url="https://curl.haxx.se"
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-cv2pdb")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-c-ares"
         "${MINGW_PACKAGE_PREFIX}-libidn2"
         "${MINGW_PACKAGE_PREFIX}-libmetalink"
         "${MINGW_PACKAGE_PREFIX}-libssh2"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-ca-certificates"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-nghttp2")
options=('!strip' 'staticlibs')
source=("${url}/download/${_realname}-${pkgver}.tar.bz2"{,.asc}
        "0001-Make-cURL-relocatable.patch"
        "0002-Only-ever-pick-CURLAUTH_BEARER-if-we-have-a-Bearer-t.patch")
sha256sums=('5f6f336921cf5b84de56afbd08dfb70adeef2303751ffb3e570c936c6d656c9c'
            'SKIP'
            '812923ce2da18fe93eb90f860055c4700c664bc8139e9da32ce1eeefc4c52ce0'
            '343cdb4d3690d409dcc58e2af3d49519816caedf464f0dd340a9cdba3c74a780')
validpgpkeys=('914C533DF9B2ADA2204F586D78E11C6B279D5C91'  # Daniel Stenberg
              '27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2'
              '4461EAF0F8E9097F48AF0555F9FEAFF9D34A1BDB')

prepare() {
  cd "${_realname}-${pkgver}"
  # MINGW builds typically mean that prepare is run twice: once for 64-bit and
  # once for 64-bit. These files are therefore added twice by below patches, so
  # let's force-delete them before trying to add them.
  rm -f lib/pathtools.h lib/pathtools.c >/dev/null 2>&1 || true
  patch -p1 -i "${srcdir}/0001-Make-cURL-relocatable.patch"
  patch -p1 -i "${srcdir}/0002-Only-ever-pick-CURLAUTH_BEARER-if-we-have-a-Bearer-t.patch"
  autoreconf -vfi
}

build() {
  local -a extra_config
  extra_config+=( --disable-debug )
  mkdir -p "${srcdir}/build-${CARCH}"
  extra_config+=("--with-ssl")
  extra_config+=("--with-ca-bundle=${MINGW_PREFIX}/ssl/certs/ca-bundle.crt")
  extra_config+=('--with-nghttp2=${MINGW_PREFIX}/')
  extra_config+=("--with-winssl")
  cd "${srcdir}/build-${CARCH}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --without-random \
    --without-librtmp \
    --enable-static \
    --enable-shared \
    --enable-sspi \
    --with-libmetalink=${MINGW_PREFIX} \
    "${extra_config[@]}"
# there's a bug with zsh completion generation script and Windows.
# curl has to specified with the file extension.
  sed -i "s|\/curl > \$\@|\/curl\$\{EXEEXT\} > \$\@|" scripts/Makefile
  make
  cv2pdb lib/.libs/libcurl-4.dll
  cv2pdb src/.libs/curl.exe
}

package() {
  cd "${srcdir}/build-${CARCH}"

  test pdb != "$1" || {
    install -d -m 755 "${pkgdir}${MINGW_PREFIX}/bin"
    install -m 755 lib/.libs/libcurl-4.pdb "${pkgdir}${MINGW_PREFIX}/bin"
    install -m 755 src/.libs/curl.pdb "${pkgdir}${MINGW_PREFIX}/bin"
    return
  }

  make DESTDIR="${pkgdir}" install
  
  local PREFIX_DEPS=$(cygpath -am ${MINGW_PREFIX})
  sed -s "s|${PREFIX_DEPS}|${MINGW_PREFIX}|g" -i ${pkgdir}${MINGW_PREFIX}/bin/curl-config
}

package_mingw-w64-i686-curl() {
  package
}

package_mingw-w64-x86_64-curl() {
  package
}

package_mingw-w64-i686-curl-pdb() {
  package pdb
}

package_mingw-w64-x86_64-curl-pdb() {
  package pdb
}
