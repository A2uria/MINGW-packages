# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libksba
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.3
pkgrel=1
pkgdesc="A CMS and X.509 access library (mingw-w64)"
arch=('any')
license=('GPL')
url="ftp://ftp.gnupg.org/gcrypt/alpha/libksba"
depends=("${MINGW_PACKAGE_PREFIX}-libgpg-error")
groups=("${MINGW_PACKAGE_PREFIX}")
options=('strip' 'staticlibs')
source=(ftp://ftp.gnupg.org/gcrypt/$_realname/$_realname-$pkgver.tar.bz2
        0001-fix-def.mingw.patch)
md5sums=('a5dd3c57fca254935f5cf8db26e39065'
         '437ad992a8ac21ba7646f707b2b4a4f2')

prepare() {
  cd $srcdir/$_realname-$pkgver
  patch -p1 -i ${srcdir}/0001-fix-def.mingw.patch
}

build() {
  [ -d ${srcdir}/build-${MINGW_CHOST} ] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  ../$_realname-$pkgver/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static
  make
}

check() {
   cd ${srcdir}/build-${MINGW_CHOST}
   make check
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make -j1 DESTDIR=$pkgdir install
}
