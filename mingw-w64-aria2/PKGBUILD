# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=aria2
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.18.10
pkgrel=1
pkgdesc="A multi-protocol & multi-source, cross platform download utility (mingw-w64)"
arch=('any')
url="http://aria2.sourceforge.net/"
license=('GPL2')
makedepends=("${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-c-ares"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-libuv")
optdepends=(
    "${MINGW_PACKAGE_PREFIX}-ruby: aria2rpc and aria2mon")
source=("http://sourceforge.net/projects/aria2/files/stable/${_realname}-${pkgver}/${_realname}-${pkgver}.tar.xz"
        'mingw-inline.patch'
        'windows-libintl.patch')
md5sums=('cf46c9f19a175366536e9815326c3891'
         '87cb82e55fb023021980e1c6994e42ff'
         '25d25975420c8ca3c3dbffe3d89fa657')

prepare() {
  cd "${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/mingw-inline.patch"
  patch -p1 -i "${srcdir}/windows-libintl.patch"
  autoreconf -fi
}

build() {
  mkdir -p "build-${CARCH}"
  cd "build-${CARCH}"

  # disabling gnutls and wintls so we will fall back to openssl
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --with-bashcompletiondir=${MINGW_PREFIX}/share/bash-completion/completions \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    --without-gnutls \
    --without-wintls \
    --enable-libaria2
  make
}

package() {
  cd "build-${CARCH}"
  make DESTDIR="${pkgdir}" install

  cd "../${_realname}-${pkgver}"

  install -Dm0755 doc/xmlrpc/aria2rpc "${pkgdir}"${MINGW_PREFIX}/bin/aria2rpc
  install -Dm0755 doc/xmlrpc/aria2mon "${pkgdir}"${MINGW_PREFIX}/bin/aria2mon
  echo '@ruby -- "%~dp0aria2rpc" %*' > "${pkgdir}"${MINGW_PREFIX}/bin/aria2rpc.cmd
  echo '@ruby -- "%~dp0aria2mon" %*' > "${pkgdir}"${MINGW_PREFIX}/bin/aria2mon.cmd
  rm "$pkgdir"${MINGW_PREFIX}/share/doc/${_realname}/xmlrpc/aria2{rpc,mon}
}
