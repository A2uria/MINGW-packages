# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libotr
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.1.0
pkgrel=1
pkgdesc="Off-the-Record Messaging Library and Toolkit (mingw-w64)"
arch=('any')
url="http://www.cypherpunks.ca/otr/"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-libgcrypt")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs' 'strip')
source=(https://otr.cypherpunks.ca/${_realname}-${pkgver}.tar.gz{,.asc}
        '002-libotr-no-undefined.patch'
        '003-fix-symlinks.patch')
md5sums=('6589f81e7ccbe244f265e8db372814a2'
         'SKIP'
         'f8bdedeb824a6394d0c5061c0aaf7f08'
         '4bedcda46f566ccece54cd68585b04f8')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/002-libotr-no-undefined.patch
  patch -p1 -i ${srcdir}/003-fix-symlinks.patch

  autoreconf -fi
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --mandir=${MINGW_PREFIX}/share/man

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make -j1 DESTDIR="${pkgdir}" install
}
