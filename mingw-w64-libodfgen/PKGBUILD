# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=libodfgen
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1.4
pkgrel=2
pkgdesc="Library to generate ODF documents (mingw-w64)"
arch=('any')
url="http://sf.net/p/libwpd/libodfgen/"
license=("LGPL 2.1")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-doxygen")
depends=("${MINGW_PACKAGE_PREFIX}-librevenge")
options=(staticlibs strip)
source=("http://sourceforge.net/projects/libwpd/files/libodfgen/${_realname}-${pkgver}/${_realname}-${pkgver}.tar.xz"
        enum-without-api-attributes.patch)
sha256sums=('249b5ed8e1acc0fee32b4381b31b4ad9453b674d4f13886bda5e9dd2b4a5f1e1'
            '3148d825f0d0778cf1b65445919b5fb89ea0c738d6c560893830f08dd5dfebfc')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/enum-without-api-attributes.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  cp -r ${srcdir}/${_realname}-${pkgver} "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --with-docs=yes

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING.LGPL" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.LGPL"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING.MPL" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.MPL"
}
