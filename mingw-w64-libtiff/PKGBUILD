# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=libtiff
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.0.3
pkgrel=6
pkgdesc="Library for manipulation of TIFF images (mingw-w64)"
arch=('any')
url="http://www.remotesensing.org/libtiff"
license=(MIT)
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         #"${MINGW_PACKAGE_PREFIX}-jbigkit"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs' 'strip')
source=("ftp://ftp.remotesensing.org/pub/libtiff/tiff-${pkgver}.tar.gz"
        libtiff-CVE-2012-4447.patch
        libtiff-CVE-2012-4564.patch
        libtiff-printdir-width.patch
        libtiff-jpeg-test.patch
        libtiff-CVE-2013-1960.patch
        libtiff-CVE-2013-1961.patch
        libtiff-manpage-update.patch
        libtiff-CVE-2013-4231.patch
        libtiff-CVE-2013-4232.patch
        libtiff-CVE-2013-4244.patch
        libtiff-make-check.patch
        libtiff-CVE-2013-4243.patch)
md5sums=('051c1068e6a0627f461948c365290410'
         '71bbe3b51f8a4e3a26cbf0af63588e4a'
         'a4b9f293f706b5668df62833cf0b56d2'
         '6a5be70d02abc33d7426dba309445529'
         'f7cbe41ac63f23e441572c6ecf4bdccc'
         'e9de577a81571ab8ffac84aac8c64381'
         'e484981da6d2366a30a89dc0217c115a'
         '39ea7f6f10ff8686757d5638c6df6882'
         '8613bb1158787567bf586a7d9f3b8da3'
         '922db499ed1de025913c960a68fc2318'
         '677f69995dd2e7710b4bca672c96ed8d'
         '60e9bbbee968b820a73a40aa425029d8'
         '74138a7605520ce47014e2ce05df1eeb')

prepare() {
  cd tiff-${pkgver}
  
  patch -p1 -i ${srcdir}/libtiff-CVE-2012-4447.patch
  patch -p1 -i ${srcdir}/libtiff-CVE-2012-4564.patch
  patch -p1 -i ${srcdir}/libtiff-printdir-width.patch
  patch -p1 -i ${srcdir}/libtiff-jpeg-test.patch
  patch -p1 -i ${srcdir}/libtiff-CVE-2013-1960.patch
  patch -p1 -i ${srcdir}/libtiff-CVE-2013-1961.patch
  patch -p1 -i ${srcdir}/libtiff-manpage-update.patch
  patch -p1 -i ${srcdir}/libtiff-CVE-2013-4231.patch
  patch -p1 -i ${srcdir}/libtiff-CVE-2013-4232.patch
  patch -p1 -i ${srcdir}/libtiff-CVE-2013-4244.patch
  patch -p1 -i ${srcdir}/libtiff-make-check.patch
  patch -p1 -i ${srcdir}/libtiff-CVE-2013-4243.patch

  rm -f libtool.m4

  libtoolize --force  --copy
  aclocal -I . -I m4
  automake --add-missing --copy
  autoconf
  autoheader
}

build() {
  cd "${srcdir}/tiff-${pkgver}"
  export CFLAGS+=" -fno-strict-aliasing"
  export CXXFLAGS+=" -fno-strict-aliasing"

  [ -d ${srcdir}/build-${MINGW_CHOST} ] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}

  ../tiff-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    --disable-jbig \
    --without-x

  make
}

check() {
  cd "${srcdir}/build-${CARCH}"
  make check
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install

  #rm -rf "${pkgdir}${MINGW_PREFIX}/share"
  cp ${srcdir}/tiff-${pkgver}/libtiff/{tiffiop,tif_dir}.h ${pkgdir}${MINGW_PREFIX}/include/
  cp libtiff/tif_config.h ${pkgdir}${MINGW_PREFIX}/include/

  # License
  # See https://fedoraproject.org/wiki/Licensing:MIT#Hylafax_Variant
  cd "${srcdir}/tiff-${pkgver}"
  install -Dm644 COPYRIGHT "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYRIGHT"
}
