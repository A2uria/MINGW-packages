# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libvpx
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4.0
pkgrel=1
pkgdesc="The VP8 Codec SDK (mingw-w64)"
arch=('any')
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-yasm")
url=('http://www.webmproject.org/')
source=(https://github.com/webmproject/libvpx/archive/v${pkgver}.tar.gz
        0001-enable-shared-on.mingw.patch
        0002-no-pthreads.mingw.patch
        0003-cross-with-native-binutils.mingw.patch
        0005-fix-exports.mingw.patch
        0006-instll-implib.mingw.patch
        0008-fix-ln-on-install.mingw.patch)
options=('strip' 'staticlibs')
md5sums=('9e82cdcb2d2146f812b71c8a7a2686a3'
         'ced1792a11e8bd446a3e910408b63bbd'
         '42a46db4240cf594217606dff3957b34'
         '9f66ab98dbbe170ca7dc0c820f56558a'
         'd33bd15169b21f5c6cba379d6c76397f'
         'c91d82639fee7d82cf69b13a34c05179'
         '1a5f3e0e8ff8126fbfd8bb133e7e1c87')

prepare() {
  cd ${srcdir}/$_realname-$pkgver

  patch -Np1 -i ${srcdir}/0001-enable-shared-on.mingw.patch
  patch -Np1 -i ${srcdir}/0002-no-pthreads.mingw.patch
  patch -Np1 -i ${srcdir}/0003-cross-with-native-binutils.mingw.patch
  patch -Np1 -i ${srcdir}/0005-fix-exports.mingw.patch
  patch -Np1 -i ${srcdir}/0006-instll-implib.mingw.patch
  patch -Np1 -i ${srcdir}/0008-fix-ln-on-install.mingw.patch
}

build() {
  if [ ${MINGW_CHOST} = "i686-w64-mingw32" ]; then
    _targetarch="x86-win32-gcc"
  else
    _targetarch="x86_64-win64-gcc"
  fi
  mkdir -p $srcdir/build-$MINGW_CHOST
  cd "$srcdir/build-$MINGW_CHOST"
  ../$_realname-$pkgver/configure \
    --prefix=${MINGW_PREFIX} \
    --target=${_targetarch} \
    --enable-vp8 \
    --enable-vp9 \
    --enable-runtime-cpu-detect \
    --enable-postproc \
    --enable-pic \
    --enable-shared \
    --enable-static \
    --disable-examples \
    --disable-docs \
    --disable-install-docs \
    --disable-install-srcs

  for _ff in *.mk; do
    sed -i "s/HAVE_GNU_STRIP=yes/HAVE_GNU_STRIP=no/g" $_ff
  done
  make -j1
}

package() {
  cd "${srcdir}/build-$MINGW_CHOST"
  make DIST_DIR="${pkgdir}${MINGW_PREFIX}" install

  rm ${pkgdir}${MINGW_PREFIX}/bin/libvpx{.dll,-1.4}*
}
