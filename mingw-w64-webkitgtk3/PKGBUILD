# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=webkitgtk
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}2" "${MINGW_PACKAGE_PREFIX}-${_realname}3")
pkgver=2.4.8
pkgrel=1
pkgdesc="GTK+ Web content engine library (mingw-w64)"
arch=('any')
url="http://webkitgtk.org/"
license=("custom")
options=('!libtool' 'strip' '!debug' 'staticlibs' 'buildflags')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-gtk2"
             "${MINGW_PACKAGE_PREFIX}-gtk3"
             "bison"
             "flex"
             "gperf"
             "libtool"
             "gtk-doc"
             "perl"
             "python2"
             "${MINGW_PACKAGE_PREFIX}-ruby"
            )
depends=("${MINGW_PACKAGE_PREFIX}-angleproject"
         "${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-enchant"
         "${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gst-plugins-base"
         "${MINGW_PACKAGE_PREFIX}-gstreamer"
         "${MINGW_PACKAGE_PREFIX}-geoclue"
         "${MINGW_PACKAGE_PREFIX}-harfbuzz"
         "${MINGW_PACKAGE_PREFIX}-icu"
         "${MINGW_PACKAGE_PREFIX}-libidn"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libsoup"
         "${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-libxslt"
         "${MINGW_PACKAGE_PREFIX}-libwebp"
         "${MINGW_PACKAGE_PREFIX}-pango"
         "${MINGW_PACKAGE_PREFIX}-xz"
        )
source=(http://webkitgtk.org/releases/webkitgtk-${pkgver}.tar.xz
        0001-webkitgtk-remove-reference-to-nonexisting-header.mingw.patch
        0002-webkitgtk-mingw-use-gcc-asm.mingw.patch
        0005-webkitgtk-mingw-compile-fix.mingw.patch
        0007-webkitgtk-mingw-fix-includes.mingw.patch
        0008-webkit-fix-angle-windows-compilation.mingw.patch
        0009-webkitgtk-mingw-m-pi.mingw.patch
        0010-webkitgtk-prevent-multiple-plugins-definitions.mingw.patch
        0011-webkitgtk-mingw-opengl.mingw.patch
        0012-webkitgtk-dont-build-test-webkit-api.mingw.patch
        0017-make-sure-correct-gcc-is-used.mingw.patch
        0018-use-la-libs-for-g-i.all.patch
        0021-use-configured-python-for-this.all.patch
        0023-use-path-instead-of-ld-library-path-on.mingw.patch
        0027-destroy-invocation-before-return-switchable.all.patch
        0029-webkit-dont-build-dump-render-tree.all.patch
        0101-webkitgtk-2.4.3-gcc-asm.all.patch
        0103-webkitgtk-2.4.3-win-format.all.patch
        0104-webkitgtk-2.4.3-misc-win-errors.all.patch
        0105-webkitgtk-2.4.3-warnings.all.patch
        0106-enable-jit.all.patch
        1001-Source.autotools.FindDependencies.m4.all.patch
        1002-preprocessor.pm-make_names.pl.all.patch
        1004-math.h_related.all.patch
        1005-misc.all.patch
        3000-export-more-symbols-on-w32.all.patch
        4000-fix-gmutexlocker-conflict.all.patch)

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-webkitgtk-remove-reference-to-nonexisting-header.mingw.patch
  patch -p1 -i ${srcdir}/0002-webkitgtk-mingw-use-gcc-asm.mingw.patch
  patch -p1 -i ${srcdir}/0005-webkitgtk-mingw-compile-fix.mingw.patch
  patch -p1 -i ${srcdir}/0007-webkitgtk-mingw-fix-includes.mingw.patch
  patch -p1 -i ${srcdir}/0008-webkit-fix-angle-windows-compilation.mingw.patch
  patch -p1 -i ${srcdir}/0009-webkitgtk-mingw-m-pi.mingw.patch
  patch -p1 -i ${srcdir}/0010-webkitgtk-prevent-multiple-plugins-definitions.mingw.patch
  patch -p1 -i ${srcdir}/0011-webkitgtk-mingw-opengl.mingw.patch
  patch -p1 -i ${srcdir}/0012-webkitgtk-dont-build-test-webkit-api.mingw.patch
  patch -p1 -i ${srcdir}/0017-make-sure-correct-gcc-is-used.mingw.patch
  patch -p1 -i ${srcdir}/0018-use-la-libs-for-g-i.all.patch
  patch -p1 -i ${srcdir}/0021-use-configured-python-for-this.all.patch
  patch -p1 -i ${srcdir}/0023-use-path-instead-of-ld-library-path-on.mingw.patch
  patch -p1 -i ${srcdir}/0027-destroy-invocation-before-return-switchable.all.patch
  patch -p1 -i ${srcdir}/0029-webkit-dont-build-dump-render-tree.all.patch
  patch -p1 -i ${srcdir}/0101-webkitgtk-2.4.3-gcc-asm.all.patch
  patch -p1 -i ${srcdir}/0103-webkitgtk-2.4.3-win-format.all.patch
  patch -p1 -i ${srcdir}/0104-webkitgtk-2.4.3-misc-win-errors.all.patch
  patch -p1 -i ${srcdir}/0105-webkitgtk-2.4.3-warnings.all.patch
  patch -p1 -i ${srcdir}/0106-enable-jit.all.patch
  patch -p1 -i ${srcdir}/1001-Source.autotools.FindDependencies.m4.all.patch
  patch -p1 -i ${srcdir}/1002-preprocessor.pm-make_names.pl.all.patch
  patch -p1 -i ${srcdir}/1004-math.h_related.all.patch
  patch -p1 -i ${srcdir}/1005-misc.all.patch
  patch -p1 -i ${srcdir}/3000-export-more-symbols-on-w32.all.patch
  patch -p1 -i ${srcdir}/4000-fix-gmutexlocker-conflict.all.patch

  autoreconf --verbose --install -I Source/autotools

  rm -rf ${srcdir}/build-gtk{2,3}
  mkdir -p ${srcdir}/build-gtk{2,3}
}

_build() {
  _ver="$1"; shift
  cd ${srcdir}/build-${_ver}

  export PYTHON=/usr/bin/python2
  export RUBY=${MINGW_PREFIX}/bin/ruby

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-debug \
    --enable-introspection \
    --disable-credential-storage \
    --disable-accelerated-compositing \
    --enable-debug-symbols=min \
    --disable-webkit2 \
    --disable-glibtest \
    --disable-static \
    --enable-shared \
    --enable-video \
    --enable-spellcheck \
    --enable-optimizations \
    --enable-geolocation \
    --disable-x11-target \
    --enable-win32-target \
    --enable-gles2=no \
    --enable-egl=no \
    "$@"

  make 
}

build() {
  local _jit_flag=
  case ${MINGW_CHOST} in
    i686*)
      _jit_flag="-enable-jit"
    ;;
    x86_64*)
      _jit_flag="-disable-jit"
    ;;
  esac
  
  _build gtk3 --disable-gtk-doc --disable-webkit2 --with-gtk=3.0 ${_jit_flag}
  _build gtk2 --disable-gtk-doc --disable-webkit2 --with-gtk=2.0 ${_jit_flag}
}

package_webkitgtk2() {
  pkgdesc+=" for GTK2"
  depends+=(${MINGW_PACKAGE_PREFIX}-gtk2)
  #provides=("${MINGW_PACKAGE_PREFIX}-libwebkit2=${pkgver}")
  cd "${srcdir}/build-gtk2"
  make -j1 DESTDIR="$pkgdir" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}

package_webkitgtk3() {
  depends+=(${MINGW_PACKAGE_PREFIX}-gtk3)
  optdepends+=("${MINGW_PACKAGE_PREFIX}-gtk2: Netscape plugin support")
  #provides=("${MINGW_PACKAGE_PREFIX}-webkitgtk3=${pkgver}" "${MINGW_PACKAGE_PREFIX}-libwebkit3=${pkgver}")
  cd "${srcdir}/build-gtk3"
  make -j1 DESTDIR="$pkgdir" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}

package_mingw-w64-i686-webkitgtk2() {
  package_webkitgtk2
}

package_mingw-w64-i686-webkitgtk3() {
  package_webkitgtk3
}

package_mingw-w64-x86_64-webkitgtk2() {
  package_webkitgtk2
}

package_mingw-w64-x86_64-webkitgtk3() {
  package_webkitgtk3
}

md5sums=('bcdcef8e1ba28dbd5bb05f86c79f76b8'
         '2ae45596c0b2d632fdd8ead551bd462b'
         'f6274cc4664f531ceb4040aa706e8ef6'
         'd09eb8edbf720c331d01a58f65eaa0a6'
         'a14fdd341b980ca21064305e11990332'
         '60f28fd809bb3aa0c8c791325bb29d3d'
         '05fe8cc73be6493d8e2269b900d03cfb'
         '11328cfd06c73e26abe6468a7dfafa23'
         'db16e2d9e6d20f9568d98371b10b8719'
         '3cdc56cfae595eefce12414f027907b7'
         'c020b0863576574f6f9e17396f4f6cfa'
         '054075647ddf38b5e2b2f8226a5043f0'
         '8277aefbe66157e4673f63105c0ff0f1'
         'ab2a01c8c34bdec57a6b4fa71d9ccdaf'
         'eeff9b56a17c8c5165aa501404d6b9f3'
         'cd60095167b0f70f26c71e2898c776a5'
         'e36f95bd1d0ffaf52046e584a7094b56'
         'dc979053d76c9c769aa5a2391523a52d'
         '03c9f2c8bdef2ef8fb42afcf47b46e05'
         '0f661078e789d307e0856ca0cb09fd15'
         '75926bb66e75c6d3158f4f4c5a74b59d'
         '58942deeaac6fabb63e51eaf2b3d65b6'
         '31ae582391c95d200b5337414e90da73'
         'dca0ab5e9a305f993ace0e519e6b6c4f'
         '1350a969b3b5c9c85c3524dd7be232e8'
         'f173518598f8c482ac946f5ff566a521'
         '6a761e17596562208cb6cd2ef6d413a2')
