# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=djvulibre
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.5.27
pkgrel=1
pkgdesc="Suite to create, manipulate and view DjVu (mingw-w64)"
arch=('any')
url="http://djvu.sourceforge.net/"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=("http://downloads.sourceforge.net/djvu/${_realname}-${pkgver}.tar.gz"
        djvulibre-3.5.27-win32.patch)
md5sums=('aa4ed331f669f5a72e3c0d7f9196c4e6'
         'b95e00f15a67df96df985b5a51b15745')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/djvulibre-3.5.27-win32.patch
  
  autoreconf -fiv
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make -j1 
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
