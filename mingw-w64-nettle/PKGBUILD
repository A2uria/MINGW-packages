# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=nettle
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.1
pkgrel=2
pkgdesc="A low-level cryptographic library (mingw-w64)"
arch=('any')
url="http://www.lysator.liu.se/~nisse/nettle"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-gmp")
options=('staticlibs' 'strip')
source=("http://www.lysator.liu.se/~nisse/archive/nettle-$pkgver.tar.gz"
        "0001-fix-GMP_NUMB_BITS-check.patch")
md5sums=('075a3699cbab48ffa2ea6f9aad91e123'
         '650c6d9784de16b8e0bb016cde260f17')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}"/0001-fix-GMP_NUMB_BITS-check.patch
  autoreconf -fiv
}

build() {
  mkdir -p "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
  cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
  "${srcdir}"/${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --enable-shared \
      --enable-public-key
  make -j1
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
  make -j1 DESTDIR="$pkgdir" install
}
