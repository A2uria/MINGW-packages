# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Maintainer: Chilledheart <rwindz0@gmail.com>

_realname=libevent
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0.22
pkgrel=2
pkgdesc="An event notification library"
arch=('any')
url="http://www.monkey.org/~provos/libevent/"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             "${MINGW_PACKAGE_PREFIX}-python2")
optdepends=("${MINGW_PACKAGE_PREFIX}-python2")
source=(https://sourceforge.net/projects/levent/files/${_realname}/${_realname}-2.0/${_realname}-${pkgver}-stable.tar.gz)
md5sums=('c4c56f986aa985677ca1db89630a2e11')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}-stable"
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}-stable/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --sysconfdir=${MINGW_PREFIX}/etc

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
