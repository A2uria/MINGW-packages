# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gobject-introspection
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-${_realname}-runtime")
pkgver=1.46.0
pkgrel=1
arch=('any')
url="https://live.gnome.org/GObjectIntrospection"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cairo"
             "${MINGW_PACKAGE_PREFIX}-glib2"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python2-mako")
options=('strip' 'staticlibs')

source=(http://ftp.gnome.org/pub/GNOME/sources/gobject-introspection/${pkgver%.*}/${_realname}-${pkgver}.tar.xz
        0018-debug-rmtree-errors.mingw.patch
        0021-tests-no-undefined.patch
        0022-change-pkg-config-invocations.mingw.patch
        0024-Support-passing-arguments-to-g-ir-scanner-through-a-.all.patch
        0050-dont-load-msvcrt.patch
        0055-fix-python-detection.patch)

md5sums=('adb40a31c7c80b65b0f4c8fd71b493dc'
         'f0ecde986ebf1f0e28b752f9c36fb6c1'
         '69ef34e2f57abc8afe1b9f6fa3786e97'
         '8d3589fb959a0a68ce7ce9051fa441fe'
         '412548a4de14aa26f27300fd6db95d41'
         'e3598d539258678eef8dde2216419faf'
         '6d809bf266e42a20b31a7acbaf0384d1')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0018-debug-rmtree-errors.mingw.patch
  patch -p1 -i ${srcdir}/0021-tests-no-undefined.patch
  patch -p1 -i ${srcdir}/0022-change-pkg-config-invocations.mingw.patch
  patch -p1 -i ${srcdir}/0024-Support-passing-arguments-to-g-ir-scanner-through-a-.all.patch
  patch -p1 -i ${srcdir}/0050-dont-load-msvcrt.patch
  patch -p1 -i ${srcdir}/0055-fix-python-detection.patch

  autoreconf -fi
}

build() {
  export PYTHON=${MINGW_PREFIX}/bin/python2
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-silent-rules

  make
}

package_gobject-introspection() {
  pkgdesc="Introspection system for GObject-based libraries (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-gobject-introspection-runtime=${pkgver}"
           "${MINGW_PACKAGE_PREFIX}-python2"
           "${MINGW_PACKAGE_PREFIX}-python2-mako")
  options=('!emptydirs')
  
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="${pkgdir}" install
  make -j1 DESTDIR="${pkgdir}" uninstall-libLTLIBRARIES uninstall-typelibsDATA
}

package_gobject-introspection-runtime() {
  pkgdesc="Introspection system for GObject-based libraries - runtime files (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-glib2")
  
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="${pkgdir}" install-libLTLIBRARIES install-typelibsDATA
}

# Wrappers
package_mingw-w64-i686-gobject-introspection() { package_gobject-introspection; }
package_mingw-w64-i686-gobject-introspection-runtime() { package_gobject-introspection-runtime; }
package_mingw-w64-x86_64-gobject-introspection() { package_gobject-introspection; }
package_mingw-w64-x86_64-gobject-introspection-runtime() { package_gobject-introspection-runtime; }
