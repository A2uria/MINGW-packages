# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Yuta Nakai <nak5124@live.jp>

_realname=x265
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.7
pkgrel=3
pkgdesc='Open Source H265/HEVC video encoder (mingw-w64)'
arch=('any')
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=('yasm' "${MINGW_PACKAGE_PREFIX}-cmake")
options=('strip')
url='https://bitbucket.org/multicoreware/x265'
source=("${_realname}-$pkgver.tar.bz2"::"$url/get/$pkgver.tar.bz2"
        install-shared-library.patch)
md5sums=('d6020c277b05ab44f8222342aaa9c99f'
         'c1dc65fc3191d6bafdd3576235ade5ae')

prepare() {
  cd "${srcdir}"/multicoreware-x265-*
  patch -p1 -i "${srcdir}"/install-shared-library.patch
}

build() {
  # Build 8 bit-depth lib and exe.
  cd "${srcdir}"/multicoreware-x265-*/build/msys
  if [ -d 8bit ]; then
    rm -fr 8bit
  fi
  mkdir -p 8bit
  cd 8bit

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ../../../source
  make

  # Build 10 bit-depth dll for x265_api_get.
  if [ -d ../10bit ]; then
    rm -fr ../10bit
  fi
  mkdir -p ../10bit
  cd ../10bit

  if [ "${CARCH}" = 'i686' ]; then
    _ENABLE_ASM=OFF
  else
    _ENABLE_ASM=ON
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DHIGH_BIT_DEPTH=ON \
    -DENABLE_ASSEMBLY=${_ENABLE_ASM} \
    -DENABLE_CLI=OFF \
    ../../../source
  make
}

package() {
  cd "${srcdir}"/multicoreware-x265-*/build/msys/8bit
  make DESTDIR=${pkgdir} install

  # Install 10 bit-depth dll.
  cd "${srcdir}"/multicoreware-x265-*/build/msys/10bit
  install -m 755 libx265.dll "${pkgdir}"/${MINGW_PREFIX}/bin/libx265_main10.dll
}
