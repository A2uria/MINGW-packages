# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>

_realname=clutter-gtk
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.6.2
pkgrel=1
pkgdesc="Clutter integration with GTK+ (mingw-w64)"
arch=('any')
url="http://clutter-project.org/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-clutter")
options=(!libtool strip staticlibs)
source=(http://download.gnome.org/sources/${_realname}/${pkgver%.*}/${_realname}-${pkgver}.tar.xz
        "0002-msys-fix-introspection-build.patch")
md5sums=('557d814c3e70cc3a2d1cf3816919b14f'
         '4b892afaf561ac2503962d238770f967')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0002-msys-fix-introspection-build.patch
  autoreconf -fvi
  #NOCONFIGURE=1 ./autogen.sh
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    --enable-silent-rules \
    --disable-gtk-doc

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="${pkgdir}" install
}
