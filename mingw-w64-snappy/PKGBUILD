# Maintainer: Norbert Pfeiler <norbert.pfeiler@gmail.com>

_realname=snappy
pkgname="$MINGW_PACKAGE_PREFIX-${_realname}"
pkgver=1.1.3
pkgrel=1
pkgdesc="A fast C++ compressor/decompressor library (mingw-w64)"
arch=('any')
license=('New BSD License')
url="hhttps://github.com/google/snappy"
depends=("$MINGW_PACKAGE_PREFIX-gcc-libs")
makedepends=("$MINGW_PACKAGE_PREFIX-gcc"
             "make")
checkdepends=("$MINGW_PACKAGE_PREFIX-lzo2"
              "$MINGW_PACKAGE_PREFIX-zlib")
source=(https://github.com/google/snappy/releases/download/${pkgver}/${_realname}-${pkgver}.tar.gz
        001-no-undefined.patch
        002-am_prog-ar.patch)
md5sums=('7358c82f133dc77798e4c2062a749b73'
         '8df88ce30eafb903bb9e51a57b947da7'
         '4c366576689cd1ad73f9daeb4cba40e7')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-no-undefined.patch
  patch -p1 -i ${srcdir}/002-am_prog-ar.patch
  autoreconf -fi
}

build() {
  [[ -d "${srcdir}"/build-${MINGW_CHOST} ]] && rm -rf "${srcdir}"/build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --prefix="$MINGW_PREFIX" \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}
  
  make
}

check() {
  cd "build-${MINGW_CHOST}"
  ./snappy_unittest.exe
}

package() {
  cd "build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
