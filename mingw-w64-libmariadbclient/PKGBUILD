# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libmariadbclient
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.1.0
pkgrel=2
pkgdesc="MariaDB client libraries (mingw-w64)"
arch=('any')
url="http://mariadb.org"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-zlib") #"${MINGW_PACKAGE_PREFIX}-sqlite3"
options=('!strip' 'staticlibs')
source=("https://downloads.mariadb.org/interstitial/connector-c-${pkgver}/source-tgz/mariadb-connector-c-${pkgver}-src.tar.gz"
        'fix-libnames-mingw.patch'
        'use_fopen_for_xp_compatibility.patch'
        'fix-size-t-defined.patch'
        'fix-qt5-uint-error.patch')
sha256sums=('568050b89463af7610d458669fd9eee06dcc9405689aca8a526ac8c013b59167'
            '6aecfea37704406d035b77702a05715493a3b9a036ffe1c2dee9fd600710d60c'
            '81ea9f16845c14a35e9c587ae84dc23c383c8be6d4afcf0587b84972a8f81b4e'
            'f0cba818d125687dc8d04f501484be8d159a1989ff12b5130c2fd213add3e519'
            'f0fcf51886f51bee17ca518ee6b0f837f9320616333fe5c016afce2d00185b82')

prepare() {
  cd ${srcdir}/mariadb-connector-c-${pkgver}-src
  patch -p0 -i ../fix-libnames-mingw.patch
  patch -p0 -i ../use_fopen_for_xp_compatibility.patch
  patch -p0 -i ../fix-size-t-defined.patch
  patch -p0 -i ../fix-qt5-uint-error.patch
}

build() {
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DWITH_EXTERNAL_ZLIB=ON \
    -DWITH_SQLITE=OFF \
    -DWITH_OPENSSL=ON \
    -DWITH_MYSQLCOMPAT=OFF \
    ../mariadb-connector-c-${pkgver}-src

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  ln -s "${pkgdir}${MINGW_PREFIX}"/include/mariadb "${pkgdir}${MINGW_PREFIX}"/include/mysql

  cp "${pkgdir}${MINGW_PREFIX}"/lib/{libmariadb,libmysqlclient}.dll.a
  cp "${pkgdir}${MINGW_PREFIX}"/lib/{libmariadb,libmysqlclient_r}.dll.a
  cp "${pkgdir}${MINGW_PREFIX}"/lib/{libmariadbclient,libmysqlclient}.a
  cp "${pkgdir}${MINGW_PREFIX}"/lib/{libmariadbclient,libmysqlclient_r}.a
}
