# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>

_realname=cogl
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.20.0
pkgrel=1
pkgdesc="An object oriented GL/GLES Abstraction/Utility Layer (mingw-w64)"
arch=('any')
url="http://www.clutter-project.org/"
license=('GPL2')
makedepends=('automake' 'libtool' 'intltool' 'itstool' 'gtk-doc' 'gnome-doc-utils' 'yelp-tools')
depends=("${MINGW_PACKAGE_PREFIX}-pango"
         "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
        )
options=(!libtool strip staticlibs)
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-${pkgver}.tar.xz
        "0001-msys2-fix-introspection-build.patch"
        "0002-windows-gl-headers.patch")
md5sums=('5b28897194d9ff76a574a9493d1f7ee0'
         '66134daa1df08ebfa143b12c08f3290e'
         '8686270ab5eae668eae11e2606aa7ce0')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-msys2-fix-introspection-build.patch
  patch -p1 -i ${srcdir}/0002-windows-gl-headers.patch

  autoreconf -fiv
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    --enable-silent-rules \
    --disable-gtk-doc

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install
}
