# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pcre2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=10.23
pkgrel=1
pkgdesc="Perl Compatible Regular Expressions (mingw-w64)"
arch=('any')
url="http://pcre.org"
license=("BSD")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         #"${MINGW_PACKAGE_PREFIX}-readline"
         "${MINGW_PACKAGE_PREFIX}-wineditline"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=(https://ftp.pcre.org/pub/pcre/${_realname}-${pkgver}.tar.bz2{,.sig}
	"0001-Fix-compilation-with-mingw64.patch"
	"0002-RunTest-skip-test-that-must-fail-on-Windows.patch"
	"0003-RunTest-skip-a-couple-more-tests-on-Windows.patch"
	"0004-RunGrepTest-compare-without-paying-attention-to-line.patch"
	"0005-RunGrepTest-skip-test-with-absolute-Unix-path.patch"
	"0006-RunGrepTest-skip-script-callout-test.patch")
sha256sums=('dfc79b918771f02d33968bd34a749ad7487fa1014aeb787fad29dd392b78c56e'
            'SKIP'
            'dae11d2f327d4bc375965b7c0eafaeb376f42788036eee6dcd7c570fa962b210'
            'd5761cbe1499cb01b9718abd8a07cd8c9ec8d49ef5e0bf13152f69d9c7c38701'
            'ccbd8d1e928db8c2efc9378dd612060a2b6c92d2c616b906668bd8348709078d'
            '69e2a1f3025432e413f73e7ab571fc53dae0400ce26ecfc4cf1493a2c852837d'
            '6c63024d42a384d5df40f4c9b16e02c5dbf62990a8733eb1fcb697408887fba1'
            'd0ccd3dc3f2c0c9c6c2db2496bc406192b05cac76a93878278a028c2008a54a1')

prepare () {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-Fix-compilation-with-mingw64.patch"
  patch -p1 -i "${srcdir}/0002-RunTest-skip-test-that-must-fail-on-Windows.patch"
  patch -p1 -i "${srcdir}/0003-RunTest-skip-a-couple-more-tests-on-Windows.patch"
  patch -p1 -i "${srcdir}/0004-RunGrepTest-compare-without-paying-attention-to-line.patch"
  patch -p1 -i "${srcdir}/0005-RunGrepTest-skip-test-with-absolute-Unix-path.patch"
  patch -p1 -i "${srcdir}/0006-RunGrepTest-skip-script-callout-test.patch"
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-unicode-properties \
    --enable-utf \
    --enable-pcre2-8 \
    --enable-pcre2-16 \
    --enable-pcre2-32 \
    --enable-pcregrep-libz \
    --enable-pcregrep-libbz2 \
    --enable-pcretest-libedit

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  # License files
  cd "${pkgdir}${MINGW_PREFIX}/share"
  mkdir -p licenses/${_realname}
  mv doc/${_realname}/COPYING licenses/${_realname}
  mv doc/${_realname}/LICENCE licenses/${_realname}
}
