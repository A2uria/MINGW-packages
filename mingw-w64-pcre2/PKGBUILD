# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pcre2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=10.36
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions (mingw-w64)"
arch=('any')
url="https://pcre.org/"
license=("BSD")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         #"${MINGW_PACKAGE_PREFIX}-readline"
         "${MINGW_PACKAGE_PREFIX}-wineditline"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=(https://ftp.pcre.org/pub/pcre/${_realname}-${pkgver}.tar.bz2{,.sig}
        0001-RunTest-skip-test-that-must-fail-on-Windows.patch
        0002-RunTest-skip-a-couple-more-tests-on-Windows.patch
        0003-RunGrepTest-compare-without-paying-attention-to-line.patch
        0004-RunGrepTest-skip-test-with-absolute-Unix-path.patch
        0005-RunGrepTest-skip-script-callout-test.patch
        0006-Fix-INTERACTIVE-fd-on-Windows.patch)
sha256sums=('a9ef39278113542968c7c73a31cfcb81aca1faa64690f400b907e8ab6b4a665c'
            'SKIP'
            'b4e506a55d54c53e4da6e4e0611fc21d0f6bf3ac1f75e3412372e8259102b80a'
            'f15d4aea1f509882a9dd67bc4889548e89f9f8210eefe3b46d591564f6a56213'
            '3304083273fa979001987fd11183c5c342ad297a50db677d3ff42a45c510b72d'
            '089d340098d337f733d3efba60808d6fafdd123f8a6b703a4304ba739130d1f9'
            'e07c1f5632be9bef9d481b51a7eca9f87f941dc3578fda662eec3df916862b7c'
            '279e33f6efdced59b037af900e5d1bf70f9a431a97801546f36d5c161ee319a2')
validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8')

prepare () {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 <"${srcdir}"/0001-RunTest-skip-test-that-must-fail-on-Windows.patch
  patch -p1 <"${srcdir}"/0002-RunTest-skip-a-couple-more-tests-on-Windows.patch
  patch -p1 <"${srcdir}"/0003-RunGrepTest-compare-without-paying-attention-to-line.patch
  patch -p1 <"${srcdir}"/0004-RunGrepTest-skip-test-with-absolute-Unix-path.patch
  patch -p1 <"${srcdir}"/0005-RunGrepTest-skip-script-callout-test.patch
  patch -p1 <"${srcdir}"/0006-Fix-INTERACTIVE-fd-on-Windows.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-jit \
    --enable-pcre2-8 \
    --enable-pcre2-16 \
    --enable-pcre2-32 \
    --enable-pcre2grep-libz \
    --enable-pcre2grep-libbz2 \
    --enable-pcre2test-libedit

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  # License files
  cd "${pkgdir}${MINGW_PREFIX}/share"
  mkdir -p licenses/${_realname}
  mv doc/${_realname}/COPYING licenses/${_realname}
  mv doc/${_realname}/LICENCE licenses/${_realname}
}
