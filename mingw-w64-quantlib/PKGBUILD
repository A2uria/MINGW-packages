# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=quantlib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.6.2
pkgrel=1
pkgdesc="QuantLib - A free/open-source library for quantitative finance (mingw-w64)"
arch=('any')
url='http://quantlib.org'
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-boost" "automake" "autoconf" "libtool")
options=('staticlibs' 'strip')
source=(https://downloads.sourceforge.net/project/quantlib/QuantLib/${pkgver}/QuantLib-${pkgver}.tar.gz
        no-undefined.patch)
md5sums=('e1831b8fc7fbf155659ec7aa8351b57a'
         'c928f002736dcdce347906e129950547')

prepare() {
  cd QuantLib-${pkgver}
  patch -p1 -i ${srcdir}/no-undefined.patch
  
  ./autogen.sh
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ../QuantLib-${pkgver}/configure \
    --disable-dependency-tracking \
    --enable-static \
    --enable-shared \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE.TXT "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
}
