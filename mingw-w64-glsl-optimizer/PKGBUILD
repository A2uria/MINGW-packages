# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=glsl-optimizer
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=r66854.14946bd
pkgrel=1
pkgdesc="C++ library that takes GLSL shaders, does some GPU-independent optimizations on them and outputs GLSL back (mingw-w64)"
arch=('any')
url="https://github.com/aras-p/glsl-optimizer"
license=('MIT')
makedepends=("git" "${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-gcc")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
options=('!strip' 'staticlibs' '!emptydirs')
source=("${_realname}"::"git+https://github.com/aras-p/glsl-optimizer.git"
        0001-GYP-Add-missing-files.patch
        0002-tests-MinGW-w64-Fix-build.patch
        0003-Win64-long-is-32-bit-use-intptr_t-instead.patch
        0004-tests-MinGW-w64-Use-find_package-OpenGL-in-CMakeList.patch
        0005-main.cpp-Remove-duplicate-_mesa_error_no_memory-func.patch
        0006-CMake-Handle-installation.patch)
md5sums=('SKIP'
         '50edec8ce23514d1347eff59ad461964'
         '0024bc916c22aeccd1d1f14cfeb9e93d'
         '24e10e6c050b3b608c15aab49d7b26b1'
         'a637926c8b3a7b73ce8059233b2d21af'
         'b12670f6a22d0e58803ed09df398be8c'
         'efb55c4ae8eee935e2ec41c8daee3930')

pkgver() {
  cd "${srcdir}"/${_realname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/${_realname}
  git am "${srcdir}"/0001-GYP-Add-missing-files.patch
  git am "${srcdir}"/0002-tests-MinGW-w64-Fix-build.patch
  git am "${srcdir}"/0003-Win64-long-is-32-bit-use-intptr_t-instead.patch
  git am "${srcdir}"/0004-tests-MinGW-w64-Use-find_package-OpenGL-in-CMakeList.patch
  git am "${srcdir}"/0005-main.cpp-Remove-duplicate-_mesa_error_no_memory-func.patch
  git am "${srcdir}"/0006-CMake-Handle-installation.patch
}

build() {
  [[ -d build-${CARCH} ]] && rm -rf build-${CARCH}
  mkdir "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ../${_realname}

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR=${pkgdir}
}
