# Maintainer: Ignacio Casal Quinteiro <icq@gnome.org>

_realname=gedit-plugins
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.20.0
pkgrel=1
arch=('any')
pkgdesc="Collection of plugins for the gedit Text Editor (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-gedit"
         "${MINGW_PACKAGE_PREFIX}-libgit2-glib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "intltool"
             "itstool"
             "gettext-devel")
options=('strip' 'staticlibs')
license=("GPL")
url="https://wiki.gnome.org/Apps/Gedit/ShippedPlugins"
install=gedit-plugins-${CARCH}.install
source=(https://download.gnome.org/sources/${_realname}/${pkgver%.*}/${_realname}-${pkgver}.tar.xz)
sha256sums=('d4902af5f9f728e0d692ca8c2f7a9f88f6c2ab8c9be037238ffb7d3aebe757bf')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  #autopoint --force
  AUTOPOINT='intltoolize --automake --copy' autoreconf -f -i
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  export PYTHON=${MINGW_PREFIX}/bin/python3
  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --disable-schemas-compile \
    --enable-python \
    --enable-vala=no

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
