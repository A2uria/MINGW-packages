# Maintainer: Ignacio Casal Quinteiro <icq@gnome.org>

_realname=gedit-plugins
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.16.0
pkgrel=2
arch=('any')
pkgdesc="Collection of plugins for the gedit Text Editor (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-gedit"
         "${MINGW_PACKAGE_PREFIX}-libgit2-glib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "intltool"
             "itstool"
             "gettext-devel")
options=('strip' 'staticlibs')
license=("GPL")
url="https://wiki.gnome.org/Apps/Gedit/ShippedPlugins"
install=gedit-plugins-${CARCH}.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-${pkgver}.tar.xz)
sha256sums=('b685b84ada8ca04f6aa201e402a0208591be4a37b874595a0a85d0a14b52f191')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  #autopoint --force
  AUTOPOINT='intltoolize --automake --copy' autoreconf -f -i
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  export PYTHON=${MINGW_PREFIX}/bin/python3
  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --disable-schemas-compile \
    --enable-python
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
