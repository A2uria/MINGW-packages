# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=eigen
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}3")
pkgver=3.2.5
pkgrel=1
pkgdesc="Lightweight C++ template library for vector and matrix math (mingw-w64)"
arch=('any')
url='http://eigen.tuxfamily.org'
license=('MPL2')
options=('strip' 'staticlibs')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-doxygen"
             "${MINGW_PACKAGE_PREFIX}-fftw"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-mpfr"
             #"${MINGW_PACKAGE_PREFIX}-qt4"
             )
source=(${_realname}-${pkgver}.tar.bz2::"http://bitbucket.org/eigen/eigen/get/${pkgver}.tar.bz2"
        'eigen-3.1.2_osversion.patch'
        'eigen-3.2_gcc58087.patch'
        'TryRunResults.cmake'
        'eigen-pkgconfig.patch'
        'eigen-test.patch')
md5sums=('21a928f6e0f1c7f24b6f63ff823593f5'
         '89587f79971efc8f363d478f48f86829'
         '1db055c7681799bb21118b2dcf95980a'
         '67e151de214f29c1e6cf00452916d7d4'
         'e07e847d7a87348b554fafe5e14b97ab'
         '84d0f7d8967a9b04bf4e0fc7613c0f56')

prepare() {
  cd "${srcdir}"/eigen-eigen-*

  patch -Np2 -i "${srcdir}/eigen-3.1.2_osversion.patch"
  patch -Np1 -i "${srcdir}/eigen-3.2_gcc58087.patch"
  patch -Np1 -i "${srcdir}/eigen-pkgconfig.patch"
  patch -Np1 -i "${srcdir}/eigen-test.patch"
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DEIGEN_BUILD_PKGCONFIG=ON \
    -DFFTW_USE_STATIC_LIBS=ON \
    -DFFTW_ROOT=${MINGW_PREFIX} \
    -C "${srcdir}"/TryRunResults.cmake \
    ../eigen-eigen-*

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
}
