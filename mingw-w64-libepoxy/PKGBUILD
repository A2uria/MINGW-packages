# Maintainer: Ignacio Casal Quinteiro <icq@gnome.org>

_realname=libepoxy
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2
pkgrel=1
pkgdesc="A library for handling OpenGL function pointer management for you. (mingw-w64)"
arch=('any')
url="https://github.com/anholt/libepoxy"
license=("MIT")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
        )
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "util-macros")
options=('strip' 'staticlibs')
source=(https://github.com/anholt/${_realname}/archive/v${pkgver}.tar.gz
        0001-135f7bf1043ca2a48d88d920fd46f3fe0097f359.all.patch
        0002-e9ce388f746f6c0557b863a38648ae1646658951.all.patch)
sha256sums=('42c328440f60a5795835c5ec4bdfc1329e75bba16b6e22b3a87ed17e9679e8f6'
            'd1569d83c2b669f167b94e091e978fd277f5ac7eebcccd75e8ca2e0f5728cee6'
            'cb5685dedbb63aa298c4017f4a764774566f6c0b076b12e355b2d5e346867761')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -Np1 -i "$srcdir/0001-135f7bf1043ca2a48d88d920fd46f3fe0097f359.all.patch"
  patch -Np1 -i "$srcdir/0002-e9ce388f746f6c0557b863a38648ae1646658951.all.patch"

  autoreconf -fi
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
}
