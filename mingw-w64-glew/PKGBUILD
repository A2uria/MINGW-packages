# Contributor: Martell Malone <martell malone at gmail dot com>

_realname=glew
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.12.0
pkgrel=3
arch=('any')
pkgdesc="GLEW, The OpenGL Extension Wrangler Library (mingw-w64)"
depends=()
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "xz")
license=('Modified BSD/MIT/GPL')
url="http://glew.sourceforge.net/"
options=('strip' '!libtool' 'staticlibs')
source=("http://sourceforge.net/projects/glew/files/glew/${pkgver}/${_realname}-${pkgver}.tgz"
        001-makefile.patch)
md5sums=('01246c7ecd135d99be031aa63f86dca1'
         '0f4ea29bfa687508ebaaefd0de5199dc')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/001-makefile.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  MSYSTEM=MINGW make GLEW_PREFIX=${MINGW_PREFIX}
}

package () {
  cd "${srcdir}/${_realname}-${pkgver}"
  MSYSTEM=MINGW make GLEW_PREFIX=${MINGW_PREFIX} DESTDIR="${pkgdir}" install.all
}
