# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libntlm
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4
pkgrel=2
pkgdesc="Library that implement Microsoft's NTLM authentication (mingw-w64)"
arch=('any')
url="http://www.nongnu.org/libntlm/"
license=("LGPL2.1")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('strip' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
source=("http://www.nongnu.org/libntlm/releases/${_realname}-${pkgver}.tar.gz"{,.sig})
md5sums=('54793488450e9085c57e0d859ff3bc8c'
         'SKIP')

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static
  make
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
