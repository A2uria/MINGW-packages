# Maintainer: Martell Malone <martellmalone@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=ag
_longname=the_silver_searcher
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.29.1.r1562.60a0b77
pkgrel=1
pkgdesc="The Silver Searcher: An attempt to make something better than ack, which itself is better than grep (mingw-w64)"
arch=('any')
url="http://geoff.greer.fm/ag"
license=("Apache")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "git")
depends=("${MINGW_PACKAGE_PREFIX}-pcre"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs') # '!strip' 'debug')
source=(#http://geoff.greer.fm/ag/releases/$_longname-$pkgver.tar.gz
        "${_realname}"::"git+https://github.com/msys2/the_silver_searcher.git"
        "0001-lang-Allow-both-extensions-and-full-filenames.patch"
        "0002-options-Fix-ordering-problems-with-color.patch"
        "0003-lang-Add-autotools-ac-am-in-m4-pc.patch"
        "0004-lang-Add-to-make-Makefile-Makefile.Debug-Makefile.Re.patch"
        "0005-lang-Add-makepkg-PKGBUILD-diff-patch-in-install.patch")
md5sums=('SKIP'
         '724af3c53b17138436b612d2221ab3c9'
         '09f7f5aaa60b59b064ee0c8059779fbb'
         'b953503903c23cfd2100bbf84d0fa097'
         '673513fd7129abf7fed400a176654577'
         '9856bce9814d618c0177bf9532f97545')

pkgver() {
  cd ${srcdir}/${_realname}
  local AC_INIT_VER=$(grep AC_INIT configure.ac -A5 | tr '\n' ' ' | sed -e 's/AC_INIT([^,]\+,\s*\[\?\([0-9.a-z]\+\).*/\1/')
  printf "%s.r%s.%s" "${AC_INIT_VER}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/${_realname}
  git am "${srcdir}"/0001-lang-Allow-both-extensions-and-full-filenames.patch
  git am "${srcdir}"/0002-options-Fix-ordering-problems-with-color.patch
  git am "${srcdir}"/0003-lang-Add-autotools-ac-am-in-m4-pc.patch
  git am "${srcdir}"/0004-lang-Add-to-make-Makefile-Makefile.Debug-Makefile.Re.patch
  git am "${srcdir}"/0005-lang-Add-makepkg-PKGBUILD-diff-patch-in-install.patch

  # configure.ac forces -O2, so force it to -O0 if debugging.
  if check_option "debug" "y"; then
    sed -i "s#-O2#-O0#g" configure.ac
  fi

  autoreconf -fi
}

build() {

  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  "${srcdir}"/${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --{build,host}=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
