# Maintainer: Ebrahim Byagowi <ebrahim@gnu.org>

_realname=ragel
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=6.9
pkgrel=2
pkgdesc="Compiles finite state machines from regular languages into executable C, C++, Objective-C, or D code (mingw-w64)"
arch=('any')
url="http://www.colm.net/open-source/ragel/"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
source=("http://www.colm.net/files/${_realname}/${_realname}-${pkgver}.tar.gz")
md5sums=('0c3110d7f17f7af4d9cb774443898dc1')

build() {
  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
  mkdir -p "build-${MINGW_CHOST}"
  cd "build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  make DESTDIR="${pkgdir}" install
  install -m0644 -D ${srcdir}/${_realname}-${pkgver}/ragel.vim "${pkgdir}${MINGW_PREFIX}"/share/vim/vimfiles/syntax/ragel.vim
}
