# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=onig
_fullname=oniguruma
pkgbase=mingw-w64-${_fullname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_fullname}"
pkgver=5.9.6
pkgrel=2
pkgdesc="A regular expressions library (mingw-w64)"
arch=('any')
license=('BSD')
url="https://github.com/kkos/oniguruma"
options=('staticlibs')
source=("https://github.com/kkos/${_fullname}/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        001-no-undefined.patch
        002-autoconf.patch)
sha256sums=('d5642010336a6f68b7f2e34b1f1cb14be333e4d95c2ac02b38c162caf44e47a7'
            '765f021ab903ab169e2f03e5bd5d37d1f9936d022ae8da949e7946d6ab1c1e0a'
            '3753717d1e7a543d03fe5077125be6556933deb7728e83b1933ebfcde162d13c')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-no-undefined.patch
  patch -p1 -i ${srcdir}/002-autoconf.patch

  touch NEWS
  touch ChangeLog
  autoreconf -fi
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --enable-shared \
      --enable-static

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make install DESTDIR="${pkgdir}"
}
