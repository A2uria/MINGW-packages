# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=libsndfile
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.0.26
pkgrel=1
pkgdesc="A C library for reading and writing files containing sampled sound (mingw-w64)"
arch=('any')
url="http://www.mega-nerd.com/libsndfile"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-flac" "${MINGW_PACKAGE_PREFIX}-libvorbis" "${MINGW_PACKAGE_PREFIX}-speex")
options=('staticlibs' 'strip')
source=(http://www.mega-nerd.com/libsndfile/files/${_realname}-${pkgver}.tar.gz
        0001-more-elegant-and-foolproof-autogen-fallback.all.patch
        0003-fix-source-searches.mingw.patch)
sha256sums=('cd6520ec763d1a45573885ecb1f8e4e42505ac12180268482a44b28484a25092'
            '3f0de6b0dca020a243b7b4b0443cdbd406b67fa06a2a344ccf074297885d63cd'
            '35633bbc54583d3f0a5dff3c3adfe95107a444ce85fc2cfc907b4bc947268c89')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-more-elegant-and-foolproof-autogen-fallback.all.patch
  patch -p1 -i ${srcdir}/0003-fix-source-searches.mingw.patch

  #sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' src/binheader_writef_check.py \
  #  src/create_symbols_file.py programs/test-sndfile-metadata-set.py
  autoreconf -fi -I M4
}

build() {
  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-sqlite \
    --disable-test-coverage \
    --enable-external-libs \
    --enable-experimental \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install

  local WINPREFIX=$(cygpath -wm ${MINGW_PREFIX})
  # Fix paths
  sed -i "s|${WINPREFIX}|${MINGW_PREFIX}|g" \
    ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/sndfile.pc
}
