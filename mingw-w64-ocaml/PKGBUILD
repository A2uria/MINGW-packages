# Maintainer: Junjie Mao <eternal.n08@gmail.com>

_realname=ocaml
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.02.1
pkgrel=1
pkgdesc="An industrial strength programming language supporting functional, imperative and object-oriented styles (mingw-w64)"
arch=('any')
url='http://ocaml.org/'
license=('Q Public Licence 1.0' 'GPL2')
depends=("${MINGW_PACKAGE_PREFIX}-flexdll")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "diffutils")
source=("http://caml.inria.fr/pub/distrib/ocaml-4.02/ocaml-4.02.1.tar.gz"
        "0002-Adapt-to-msys2-directory-layout.patch"
        "0003-Install-manuals-in-msys2.patch"
        "0004-camlheader-has-the-extension-.exe-in-msys2.patch"
        "0005-Install-libraries-to-lib-ocaml-instead-of-lib.patch")
sha1sums=('6af8c67f2badece81d8e1d1ce70568a16e42313e'
          'ee16e23932922f6f9fcc48072d564b90a3b66246'
          'a780a6c94aaedcd0d19a7fbc26e09776b581a8af'
          'e1f6b201c25b61d39404406c82684f6c8d70d23a'
          '17010515bd9715ed36300165643d8f3ad01288cf')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  mv config/Makefile.mingw{,32}
  cp config/m-nt.h config/m.h
  cp config/s-nt.h config/s.h

  patch -p1 -i ${srcdir}/0002-Adapt-to-msys2-directory-layout.patch
  patch -p1 -i ${srcdir}/0003-Install-manuals-in-msys2.patch
  patch -p1 -i ${srcdir}/0004-camlheader-has-the-extension-.exe-in-msys2.patch
  patch -p1 -i ${srcdir}/0005-Install-libraries-to-lib-ocaml-instead-of-lib.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  cp -rf ${_realname}-${pkgver} build-${MINGW_CHOST}
  
  cd ${srcdir}/build-${MINGW_CHOST}
  cp config/Makefile.${MINGW_PREFIX#/} config/Makefile

  make -f Makefile.nt world PREFIX="${MINGW_PREFIX}" TOOLPREF= -j1
  make -f Makefile.nt bootstrap PREFIX="${MINGW_PREFIX}" TOOLPREF= -j1
  make -f Makefile.nt opt PREFIX="${MINGW_PREFIX}" TOOLPREF= -j1
  make -f Makefile.nt opt.opt PREFIX="${MINGW_PREFIX}" TOOLPREF= -j1
}

check() {
  # The testsuite refuses to compile due to lack of headers such as sys/resource.h
  plain "skip"
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make -f Makefile.nt install TOOLPREF= PREFIX="${pkgdir}${MINGW_PREFIX}" -j1
  [ -f LICENSE ] && install -Dm644 LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE

  mkdir -p ${pkgdir}${MINGW_PREFIX}/etc/profile.d
  echo '[ "$MSYSTEM" = "'$(echo ${MINGW_PREFIX#/} | tr "[:lower:]" "[:upper:]")'" ] && export OCAMLLIB='${MINGW_PREFIX}'/lib/ocaml' > ${pkgdir}${MINGW_PREFIX}/etc/profile.d/ocaml.sh
}
