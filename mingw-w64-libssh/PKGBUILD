# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libssh
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.6.5
pkgrel=1
pkgdesc="Library for accessing ssh client services through C libraries (mingw-w64)"
arch=('any')
url="http://www.libssh.org/"
license=("LGPL")
install=libssh-${CARCH}.install
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-cmake")
depends=("${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-zlib")
groups=("${MINGW_PACKAGE_PREFIX}")
options=('staticlibs' 'strip')
validpgpkeys=('8DFF53E18F2ABC8D8F3C92237EE0FC4DCC014E3D') # Andreas Schneider <asn@cryptomilk.org>
source=(https://red.libssh.org/attachments/download/121/${_realname}-${pkgver}.tar.xz
        https://red.libssh.org/attachments/download/120/${_realname}-${pkgver}.tar.asc
        mingw-as-unix.patch
        mingw-DATADIR-conflict.patch
        fix-threads-libraryname-in-config.patch)
md5sums=('fdf107011ae1847d86620b39ae37d4f3'
         'SKIP'
         'd819f18cb4ae07ad2a6589b02765b6c2'
         '7be13e00d27ce77f862012ee0654019b'
         '34899219c2804f81a2218ba13847122b')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/mingw-as-unix.patch
  patch -p1 -i ${srcdir}/mingw-DATADIR-conflict.patch
  patch -p1 -i ${srcdir}/fix-threads-libraryname-in-config.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_STATIC_LIB=ON \
    ../${_realname}-${pkgver}

  make all
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make install

  local PREFIX_WIN=$(cygpath -wm ${pkgdir}${MINGW_PREFIX})
  sed -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libssh.pc \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libssh_threads.pc \
    -i ${pkgdir}${MINGW_PREFIX}/lib/cmake/libssh/libssh-config.cmake
}
