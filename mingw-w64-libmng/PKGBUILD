# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libmng
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0.3
pkgrel=3
pkgdesc="A collection of routines used to create and manipulate MNG format graphics files (mingw-w64)"
arch=('any')
url="http://sourceforge.net/projects/libmng"
license=("zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake")
depends=("${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('strip' 'staticlibs')
source=("http://downloads.sourceforge.net/libmng/${_realname}-${pkgver}.tar.xz"{,.asc}
         libmng-1.0.10-mingw.patch
         libmng-1.0.9-dont-leak-zlib-streams.diff
         no-undefined.patch
         libmng-2.0.3-cmake-versioning.patch
         fix-mng-version-cmake.patch
         pkg-config-extra-flags.patch)
md5sums=('e9e899adb1b681b17f14d91e261878c5'
         'SKIP'
         'cb375eee738835855615afd5c056252e'
         '9a3638775d5efaa1111973b40f4f96a4'
         'e4614f8cd3c813b38340468b2277c3ae'
         '3dff90a06b3ddb372d0a3dd15e08c713'
         '3fd61b6c7814bb30dc03cf2b65280a0b'
         'eab95494c678f84164a6b6bd418732b9')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p0 -i "$srcdir"/libmng-1.0.9-dont-leak-zlib-streams.diff
  patch -p1 -i "$srcdir"/libmng-1.0.10-mingw.patch
  patch -p1 -i "$srcdir"/no-undefined.patch
  patch -p1 -i "$srcdir"/libmng-2.0.3-cmake-versioning.patch
  patch -p1 -i "$srcdir"/fix-mng-version-cmake.patch
  patch -p1 -i "$srcdir"/pkg-config-extra-flags.patch
}

build() {
  [ -d ${srcdir}/build-${MINGW_CHOST} ] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd ${srcdir}/build-${MINGW_CHOST}
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_STATIC_LIBS=ON \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
