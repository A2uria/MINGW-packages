# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libwebp
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.4.3
pkgrel=2
pkgdesc="the WebP library. Includes conversion tools (mingw-w64)"
arch=('any')
url="https://developers.google.com/speed/webp/"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-giflib"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtiff")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
source=(http://downloads.webmproject.org/releases/webp/${_realname}-${pkgver}.tar.gz{,.asc})
md5sums=('08813525eeeffe7e305b4cbfade8ae9b'
         'SKIP')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
}

build() {
  mkdir -p ${srcdir}/${MINGW_CHOST}
  cd "${srcdir}/${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-static \
    --enable-swap-16bit-csp \
    --enable-experimental \
    --enable-libwebpmux \
    --enable-libwebpdemux \
    --enable-libwebpdecoder
  make
}

package() {
  cd "${srcdir}/$MINGW_CHOST"
  make install DESTDIR="${pkgdir}"
}
